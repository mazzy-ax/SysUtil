Exportfile for AOT version 1.0 or later
Formatversion: 1

***Element: CLS

; Microsoft Dynamics AX Class: Any unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #Any
    PROPERTIES
      Name                #Any
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #addToTextBuffer
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// рекурсивно заполняет textBuffer
        #protected textBuffer addToTextBuffer(
        #    str         _sep            = ',',
        #    boolean     _queryValue     = false,
        #    TextBuffer  _textBuffer     = null)
        #{
        #    Types   type    = typeof(value);
        #    int     i;
        #    int     len;
        #    str     s;
        #
        #    if (_textBuffer == null)
        #    {
        #        _textBuffer = new TextBuffer();
        #    }
        #
        #    switch(type)
        #    {
        #        case Types::Container:
        #            len = conlen(value);
        #            for(i=1; i<=len; ++i)
        #            {
        #                if(i>1)
        #                {
        #                    _textBuffer.appendText(_sep);
        #                }
        #                _textBuffer = Any::conpeek(value, i).addToTextBuffer(_sep, _queryValue, _textBuffer);
        #            }
        #            break;
        #
        #        case Types::Class:
        #            if (SysDictClass::isEqualOrSuperclass(classidget(value), classnum(Set)))
        #            {
        #                _textBuffer = Any::construct(ConUtil::fromSet(value)).addToTextBuffer(_sep, _queryValue, _textBuffer);
        #                break;
        #            }
        #            else if (SysDictClass::isEqualOrSuperclass(classidget(value), classnum(List)))
        #            {
        #                _textBuffer = Any::construct(ConUtil::fromList(value)).addToTextBuffer(_sep, _queryValue, _textBuffer);
        #                break;
        #            }
        #            else if (SysDictClass::isEqualOrSuperclass(classidget(value), classnum(Array)))
        #            {
        #                _textBuffer = Any::construct(ConUtil::fromArray(value)).addToTextBuffer(_sep, _queryValue, _textBuffer);
        #                break;
        #            }
        #            // no break! проваливаемся ниже
        #        case Types::Record:
        #        case Types::AnyType:
        #        case Types::BLOB:
        #        case Types::void:
        #            throw error(Error::unsupportedEnum(funcname(), type));
        #
        #        default:
        #            s = _queryValue ? SysQuery::value(value) : strfmt('%1', value);
        #            _textBuffer.appendText(s);
        #            break;
        #    }
        #
        #    return _textBuffer;
        #}
      ENDSOURCE
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// класс содержит неизменяемое значение любого типа
        #// чтобы переприсвоить значение создайте новый объект этого типа
        #//
        #// Чем отличается этот класс от Anytype и от SysAnyType?
        #// 1.
        #// Аксапта позволяет установить произвольное значение в переменную типа anytype только один раз.
        #// Во время первой инициализации тип переменной фиксируется, в дальнейшем тип изменить нельзя.
        #//
        #// Anytype var = 1;
        #// var = 2;         // ok
        #// var = '';        // результат не определен. В большинстве версий аксапты будет 0;
        #//
        #// 2.
        #// Чтобы обойти эту проблему в Аксапту ввели тип SysAnyType
        #// SysAnyType позволяет в любой момент установить действительно любое значение.
        #// Но ради этого тип сделали очень тяжелым - в куче хранится map, key и сам объект в качестве value
        #// кроме того, каждое обращение к value - это lookup внутри map
        #//
        #// SysAnyType var = new SysAnyType(1);
        #// var.value('');   // будет хранить пустую строку
        #//
        #// 3.
        #// Данный класс Any не позволяет изменять значение хранимого объекта.
        #// Поэтому данный класс может просто хранить ref на объект произвольного типа
        #// Но! переприсвоить значение можно просто пересоздав объект Any
        #//
        #// Any var = new Any(1);        // ok
        #// var = new Any('');           // ok
        #//
        #// у Any есть несколько специализированных конструкторов и обычный construct
        #//
        #// Any var = Any::constuct(1);  // ok
        #// var = Any::constuct('');     // ok
        #//
        #// Класс проявляется в цикле (обратите внимание на специализированный конструктор conpeek)
        #//
        #// container con = [1, '', 31\01\2019];
        #// Any var;
        #// for(i=1; i<=conlen(con); ++i)
        #// {
        #//     var = Any::conpeek(con, i);
        #// }
        #class Any
        #{
        #    Anytype value;
        #}
      ENDSOURCE
      SOURCE #ensureEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Any ensureEmpty()
        #{
        #    if (this.value())
        #    {
        #        throw error(Error::wrongUseOfFunction(funcname()));
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #ensureInTypes
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Any ensureInTypes(container typeCon)
        #{
        #    if (confind(typeCon, this.type()))
        #    {
        #        return this;
        #    }
        #
        #    throw error(Error::wrongUseOfFunction(funcname()));
        #}
      ENDSOURCE
      SOURCE #ensureNotEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Any ensureNotEmpty()
        #{
        #    if (this.value())
        #    {
        #        return this;
        #    }
        #
        #    throw error(Error::wrongUseOfFunction(funcname()));
        #}
      ENDSOURCE
      SOURCE #ensureType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Any ensureType(Types type, int potentialAncestorId = 0)
        #{
        #    if (AnytypeUtil::isType(this.value(), type, potentialAncestorId))
        #    {
        #        return this;
        #    }
        #
        #    throw error(Error::wrongUseOfFunction(funcname()));
        #}
      ENDSOURCE
      SOURCE #equalsTo
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean equalsTo(Any type2)
        #{
        #    //Is one of them null ?
        #    if (!type2)
        #    {
        #        return false;
        #    }
        #
        #    //Are their types different ?
        #    if (this.type() != type2.type())
        #    {
        #        return false;
        #    }
        #
        #    //They are the same type. Is this type void ?
        #    if (this.type() == Types::void)
        #    {
        #        return true;
        #    }
        #
        #    //Are their values different ?
        #    if (this.value() != type2.value())
        #    {
        #        return false;
        #    }
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void new(Anytype _value = null)
        #{
        #    value = _value;
        #}
      ENDSOURCE
      SOURCE #toNullValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public anytype toNullValue()
        #{
        #    return nullValueBaseType(this.type());
        #}
      ENDSOURCE
      SOURCE #toQueryValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toQueryValue()
        #{
        #    str ret;
        #
        #    switch (this.type())
        #    {
        #        case Types::Class:
        #        case Types::Container:
        #            ret = this.addToTextBuffer(',', true).getText();
        #            break;
        #
        #        case Types::Record:
        #        case Types::AnyType:
        #        case Types::BLOB:
        #        case Types::void:
        #            throw error(Error::unsupportedEnum(funcname(), this.type()));
        #
        #        default:
        #            ret = SysQuery::value(value);
        #            break;
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toString(str sep = ',')
        #{
        #    // поскольку этот метод может вызваться в отладчике еще до инициализации,
        #    // то в этом методе нельзя использовать методы класса и нельзя бросать исключение
        #    // и ни в коем случае не бросаем исключение на неинициализированные значения
        #    str ret = AnytypeUtil::toString(this.value(), sep);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #type
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #final public Types type()
        #{
        #    Types type = typeof(value);
        #
        #    return type;
        #}
      ENDSOURCE
      SOURCE #value
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// только чтение! см. комментарий к classDeclaration
        #final public AnyType value()
        #{
        #    return value;
        #}
      ENDSOURCE
      SOURCE #checkAllNotEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// принимает данные в контейнере, который состоит из контейнеров - значение и метка
        #// выдает ошибки для всех пустых значений
        #// если хотя бы одно значение пустое, то возвращает false
        #// если все значения не пустые, то возвращает true
        #//
        #// пример использования:
        #//     Any::checkAllNotEmpty([[purchId, 'Закупка'], [emplId, 'Ответственный']]);
        #//
        #public static boolean checkAllNotEmpty(container pairs, str txt = 'Значение не должно быть пустым. Укажите значение для %1.')
        #{
        #    boolean     ret = true;
        #
        #    container   pair;
        #    str         label;
        #
        #    int         i;
        #    int         len = conlen(pairs);
        #    ;
        #
        #    for (i=1; i<=len; ++i)
        #    {
        #        pair  = ConUtil::peekAsContainer(pairs, i);
        #        label = conpeek(pair, 2);
        #
        #        if( !conpeek(pair, 1) )
        #        {
        #            ret = CheckFailed(strfmt(txt, label));
        #        }
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #conpeek
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Any conpeek(container con, int idx)
        #{
        #    anytype value   = conpeek(con, idx);  // не делаем дополнительных проверок: к случае чего, аксапта выбросит исключение про контейнеры
        #    Any     ret     = Any::construct(value);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Any construct(Anytype value)
        #{
        #    return new Any(value);
        #}
      ENDSOURCE
      SOURCE #field
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Any field(Common record, fieldId fieldid)
        #{
        #    anytype value   = record.(fieldid);  // не делаем дополнительных проверок: к случае чего, аксапта выбросит исключение про поля в записи
        #    Any     ret     = Any::construct(value);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isEqual
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isEqual(Any type1, Any type2)
        #{
        #    //Are both null ?
        #    if (!type1 && !type2)
        #    {
        #        return true;
        #    }
        #
        #    //Is one of them null ?
        #    if (!type1 || !type2)
        #    {
        #        return false;
        #    }
        #
        #    //Are their types different ?
        #    if (type1.type() != type2.type())
        #    {
        #        return false;
        #    }
        #
        #    //They are the same type. Is this type void ?
        #    if (type1.type() == Types::void)
        #    {
        #        return true;
        #    }
        #
        #    //Are their values different ?
        #    if (type1.value() != type2.value())
        #    {
        #        return false;
        #    }
        #
        #    return true;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: AnyTest unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #AnyTest
    PROPERTIES
      Name                #AnyTest
      Extends             #SysTestCase
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class AnyTest extends SysTestCase
        #{
        #}
      ENDSOURCE
      SOURCE #testConpeek
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testConpeek()
        #{
        #    container con = [1, '', 31\01\2019, ['inside'], 2019-01-31T13:00:00];
        #    Any any;
        #    ;
        #
        #    any = Any::conpeek(con, 1);
        #    this.assertEquals(conpeek(con,1), any.value());
        #
        #    any = Any::conpeek(con, 2);
        #    this.assertEquals(conpeek(con, 2), any.value());
        #
        #    any = Any::conpeek(con, 3);
        #    this.assertEquals(conpeek(con, 3), any.value());
        #
        #    any = Any::conpeek(con, 4);
        #    this.assertEquals(conpeek(con, 4), any.value());
        #
        #    any = Any::conpeek(con, 5);
        #    this.assertEquals(conpeek(con, 5), any.value());
        #}
      ENDSOURCE
      SOURCE #testConstruct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testConstruct()
        #{
        #    //TO DO проверить все типы
        #    int i = 1;
        #    str s = 'test';
        #    Voucher v = 'voucher';
        #    Any any;
        #    ;
        #
        #    any = Any::construct(i);
        #    this.assertEquals(i, any.value());
        #
        #    any = Any::construct(s);
        #    this.assertEquals(s, any.value());
        #
        #    any = Any::construct(v);
        #    this.assertEquals(v, any.value());
        #}
      ENDSOURCE
      SOURCE #testEqualsTo
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testEqualsTo()
        #{
        #    //TO DO проверить все типы
        #    int i = 1;
        #    str s = 'test';
        #    Voucher v = 'voucher';
        #    Date d = systemdateget();
        #    utcdatetime dt = DateTimeUtil::utcNow();
        #    container c = [1, '', d];
        #
        #    Any any1 = Any::construct(i);
        #    Any any2;
        #    ;
        #
        #    any2 = Any::construct(i);
        #    this.assertTrue(any1.equalsTo(any2));
        #    this.assertTrue(any2.equalsTo(any1));
        #
        #    this.assertFalse(any1.equalsTo(null));
        #    this.assertFalse(any2.equalsTo(null));
        #
        #    any2 = Any::construct(s);
        #    this.assertFalse(any1.equalsTo(any2));
        #    this.assertFalse(any2.equalsTo(any1));
        #
        #    any2 = Any::construct(v);
        #    this.assertFalse(any1.equalsTo(any2));
        #    this.assertFalse(any2.equalsTo(any1));
        #
        #    any2 = Any::construct(d);
        #    this.assertFalse(any1.equalsTo(any2));
        #    this.assertFalse(any2.equalsTo(any1));
        #
        #    any2 = Any::construct(dt);
        #    this.assertFalse(any1.equalsTo(any2));
        #    this.assertFalse(any2.equalsTo(any1));
        #
        #    any2 = Any::construct(c);
        #    this.assertFalse(any1.equalsTo(any2));
        #    this.assertFalse(any2.equalsTo(any1));
        #}
      ENDSOURCE
      SOURCE #testField
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testField()
        #{
        #    UserInfo userInfo;
        #    Any any;
        #    ;
        #
        #    select firstOnly userInfo;
        #
        #    any = Any::field(userInfo, fieldnum(Userinfo, RecId));
        #    this.assertEquals(userInfo.RecId, any.value());
        #
        #    any = Any::field(userInfo, fieldnum(Userinfo, dataAreaId));
        #    this.assertEquals(userInfo.dataAreaId, any.value());
        #
        #    any = Any::field(userInfo, fieldnum(Userinfo, createdDateTime));
        #    this.assertEquals(userInfo.createdDateTime, any.value());
        #}
      ENDSOURCE
      SOURCE #testIsEqual
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsEqual()
        #{
        #    //TO DO проверить все типы
        #    int i = 1;
        #    str s = 'test';
        #    Voucher v = 'voucher';
        #    Date d = systemdateget();
        #    utcdatetime dt = DateTimeUtil::utcNow();
        #    container c = [1, '', d];
        #
        #    Any any1 = Any::construct(i);
        #    Any any2;
        #    ;
        #
        #    any2 = Any::construct(i);
        #    this.assertTrue(Any::isEqual(any1, any2));
        #    this.assertTrue(Any::isEqual(any2, any1));
        #
        #    this.assertTrue(Any::isEqual(null, null));
        #    this.assertFalse(Any::isEqual(any1, null));
        #    this.assertFalse(Any::isEqual(null, any2));
        #
        #    any2 = Any::construct(s);
        #    this.assertFalse(Any::isEqual(any1, any2));
        #    this.assertFalse(Any::isEqual(any2, any1));
        #
        #    any2 = Any::construct(v);
        #    this.assertFalse(Any::isEqual(any1, any2));
        #    this.assertFalse(Any::isEqual(any2, any1));
        #
        #    any2 = Any::construct(d);
        #    this.assertFalse(Any::isEqual(any1, any2));
        #    this.assertFalse(Any::isEqual(any2, any1));
        #
        #    any2 = Any::construct(dt);
        #    this.assertFalse(Any::isEqual(any1, any2));
        #    this.assertFalse(Any::isEqual(any2, any1));
        #
        #    any2 = Any::construct(c);
        #    this.assertFalse(Any::isEqual(any1, any2));
        #    this.assertFalse(Any::isEqual(any2, any1));
        #}
      ENDSOURCE
      SOURCE #testToString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testToString()
        #{
        #    //TO DO проверить все типы
        #    Any any;
        #    ;
        #
        #    any = Any::construct(1);
        #    this.assertEquals('1', any.toString());
        #
        #    any = Any::construct('test');
        #    this.assertEquals('test', any.toString());
        #
        #    any = Any::construct(31\12\2019);
        #    this.assertEquals(strfmt('%1',31\12\2019), any.toString());
        #
        #    any = Any::construct(2019-12-31T13:01:00);
        #    this.assertEquals(strfmt('%1',2019-12-31T13:01:00), any.toString());
        #
        #    any = Any::construct([1, '', [31\12\2019]]);
        #    this.assertEquals('1,,31.12.2019', any.toString());
        #
        #    any = Any::construct(appl);
        #    this.assertEquals('Class', any.toString());
        #
        #    any = Any::construct(SysUserInfo::find());
        #    this.assertEquals('Record', any.toString());
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: AnyPrevCurrent unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #AnyPrevCurrent
    PROPERTIES
      Name                #AnyPrevCurrent
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #changed
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// сюда можно передать параметр любого типа, включая объект типа Any
        #final public boolean changed(anytype newCurrent = null)
        #{
        #    boolean ret;
        #
        #    if( !prmisdefault(newCurrent) )
        #    {
        #        this.current(newCurrent);
        #    }
        #
        #    ret = prev && !Any::isEqual(curr, prev);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #final class AnyPrevCurrent
        #{
        #    Any curr;
        #    Any prev;
        #}
      ENDSOURCE
      SOURCE #current
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// сюда можно передать параметр любого типа, включая объект типа Any
        #final public anytype current(anytype newCurrent = null)
        #{
        #    anytype ret;
        #
        #    if( !prmIsDefault(newCurrent) )
        #    {
        #        prev = curr;
        #        curr = Any::construct(newCurrent);
        #    }
        #
        #    if( curr )
        #    {
        #        ret = curr.value();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #prev
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// сюда можно передать параметр любого типа, включая объект типа Any
        #final public anytype prev(anytype newPrev = null)
        #{
        #    anytype ret;
        #
        #    if( !prmIsDefault(newPrev) )
        #    {
        #        prev = Any::construct(newPrev);
        #    }
        #
        #    if( prev )
        #    {
        #        ret = prev.value();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #reset
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void reset()
        #{
        #    prev = null;
        #    curr = null;
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static AnyPrevCurrent construct()
        #{
        #    return new AnyPrevCurrent();
        #}
        #
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: AnytypeUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #AnytypeUtil
    PROPERTIES
      Name                #AnytypeUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// класс реализует только статические методы.
        #// не содержит внутри никаких значений, не имеет смысла создавать экземпляр этого класса
        #//
        #class AnytypeUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #as
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype as(anytype value, anytype asValue)
        #{
        #    Types   asType = typeof(asValue);
        #    Int     asId   = AnytypeUtil::id(asValue);
        #    anytype ret    = AnytypeUtil::asType(value, asType, asId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #asArray
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Array asArray(anytype value)
        #{
        #    if( AnytypeUtil::isArray(value) )
        #    {
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asClass
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Object asClass(anytype value, ClassId potentialAncestorId = 0)
        #{
        #    if( typeof(value) == Types::Class )
        #    {
        #        if( potentialAncestorId )
        #        {
        #            return SysDictClass::as(value, potentialAncestorId);
        #        }
        #
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asCollection
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asCollection(anytype value)
        #{
        #    return EnumeratorUtil::asEnumerable(value);
        #}
      ENDSOURCE
      SOURCE #asContainer
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container asContainer(anytype value)
        #{
        #    if( typeof(value) == Types::Container )
        #    {
        #        return value;
        #    }
        #
        #    return connull();
        #}
      ENDSOURCE
      SOURCE #asDate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Date asDate(anytype value)
        #{
        #    if( typeof(value) == Types::Date )
        #    {
        #        return value;
        #    }
        #
        #    return DateNull();
        #}
      ENDSOURCE
      SOURCE #asDateTime
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static UtcDateTime asDateTime(anytype value)
        #{
        #    if( typeof(value) == Types::UtcDateTime )
        #    {
        #        return value;
        #    }
        #
        #    return utcDateTimeNull();
        #}
      ENDSOURCE
      SOURCE #asEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asEnum(anytype value, enumId enumId = 0)
        #{
        #    return AnytypeUtil::asType(value, Types::Enum, enumId);
        #}
      ENDSOURCE
      SOURCE #asEnumDefault
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asEnumDefault(anytype value, anytype defaultEnumValue)
        #{
        #    int enumId = AnytypeUtil::enumId(defaultEnumValue);
        #
        #    if( !enumId )
        #    {
        #        throw error(strfmt('The parameter defaultEnumValue should be Enum. The type of defaultEnumValue is %1.', typeof(defaultEnumValue)));
        #    }
        #
        #    if( AnytypeUtil::isType(value, Types::Enum, enumId) )
        #    {
        #        return value;
        #    }
        #
        #    return defaultEnumValue;
        #}
        #
        #
      ENDSOURCE
      SOURCE #asEnumerable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asEnumerable(anytype value)
        #{
        #    return EnumeratorUtil::asEnumerable(value);
        #}
      ENDSOURCE
      SOURCE #asEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asEnumerator(anytype value)
        #{
        #    return EnumeratorUtil::asEnumerator(value);
        #}
      ENDSOURCE
      SOURCE #asGUID
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static GUID asGUID(anytype value)
        #{
        #    if( typeof(value) == Types::Guid )
        #    {
        #        return value;
        #    }
        #
        #    return nullValueBaseType(Types::Guid);
        #}
      ENDSOURCE
      SOURCE #asInt
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int asInt(anytype value)
        #{
        #    if( typeof(value) == Types::Integer )
        #    {
        #        return value;
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #asInt64
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int64 asInt64(anytype value)
        #{
        #    if( typeof(value) == Types::Int64 )
        #    {
        #        return value;
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #asList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static List asList(anytype value)
        #{
        #    if( AnytypeUtil::isList(value) )
        #    {
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asMap
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Map asMap(anytype value)
        #{
        #    if( AnytypeUtil::isMap(value) )
        #    {
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asObject
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Object asObject(anytype value)
        #{
        #    return AnytypeUtil::asClass(value);
        #}
      ENDSOURCE
      SOURCE #asReal
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Real asReal(anytype value)
        #{
        #    if( typeof(value) == Types::Real )
        #    {
        #        return value;
        #    }
        #
        #    return 0.0;
        #}
      ENDSOURCE
      SOURCE #asRecId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int64 asRecId(anytype value)
        #{
        #    if( typeof(value) == Types::Int64 )
        #    {
        #        return value;
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #asRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common asRecord(anytype value, TableId tableId = 0)
        #{
        #    Common ret = AnytypeUtil::asType(value, Types::Record, tableId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #asRecVersion
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int64 asRecVersion(anytype value)
        #{
        #    if( typeof(value) == Types::Integer )
        #    {
        #        return value;
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #assertEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype assertEmpty(anytype value)
        #{
        #    if (value)
        #    {
        #        throw error(Error::wrongUseOfFunction(funcName()));
        #    }
        #
        #    return value;
        #}
      ENDSOURCE
      SOURCE #assertInTypes
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype assertInTypes(anytype value, container typeCon)
        #{
        #    if (confind(typeCon, typeof(value)) )
        #    {
        #        return value;
        #    }
        #
        #    throw error(Error::wrongUseOfFunction(funcname()));
        #}
      ENDSOURCE
      SOURCE #assertNotEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype assertNotEmpty(anytype value)
        #{
        #    if (value)
        #    {
        #        return value;
        #    }
        #
        #    throw error(Error::wrongUseOfFunction(funcName()));
        #}
      ENDSOURCE
      SOURCE #assertType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype assertType(anytype value, Types type, int potentialAncestorId = 0)
        #{
        #    if (AnytypeUtil::isType(value, type, potentialAncestorId))
        #    {
        #        return value;
        #    }
        #
        #    throw error(Error::wrongUseOfFunction(funcName()));
        #}
      ENDSOURCE
      SOURCE #asSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Set asSet(anytype value)
        #{
        #    if( AnytypeUtil::isSet(value) )
        #    {
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str asString(anytype value)
        #{
        #    if( AnytypeUtil::isString(value) )
        #    {
        #        return value;
        #    }
        #
        #    return '';
        #}
      ENDSOURCE
      SOURCE #asTime
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static timeOfDay asTime(anytype value)
        #{
        #    if( typeof(value) == Types::Integer )
        #    {
        #        return value;
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #asType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asType(anytype value, Types type, int potentialAncestorId = 0)
        #{
        #    if( AnytypeUtil::isType(value, type, potentialAncestorId) )
        #    {
        #        return value;
        #    }
        #
        #    return nullValueBaseType(type);
        #}
      ENDSOURCE
      SOURCE #classId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int classId(anytype value)
        #{
        #    int id = 0;
        #
        #    if( typeof(value) == Types::Class )
        #    {
        #        id = classidget(value);
        #    }
        #
        #    return id;
        #}
      ENDSOURCE
      SOURCE #enumId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int enumId(anytype value)
        #{
        #    int id = 0;
        #
        #    if( typeof(value) == Types::Enum )
        #    {
        #        id = DictEnum::value2id(value);
        #    }
        #
        #    return id;
        #}
      ENDSOURCE
      SOURCE #getEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает объект, у которого можно вызвать метод moveNext
        #public static Enumerator getEnumerator(anytype value)
        #{
        #    return EnumeratorUtil::getEnumerator(value);
        #}
      ENDSOURCE
      SOURCE #id
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int id(anytype value)
        #{
        #    int   id   = 0;
        #    Types type = typeof(value);
        #
        #    switch( type )
        #    {
        #        case Types::Enum:
        #            id = DictEnum::value2id(value);
        #            break;
        #
        #        case Types::Record:
        #            id = AnytypeUtil::asRecord(value).TableId;
        #            break;
        #
        #        case Types::Class:
        #            id = classidget(value);
        #            break;
        #    }
        #
        #    return id;
        #}
      ENDSOURCE
      SOURCE #is
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean is(anytype value, anytype isValue)
        #{
        #    Types   isType = typeof(isValue);
        #    Int     isId   = AnytypeUtil::id(isValue);
        #    boolean ret    = AnytypeUtil::isType(value, isType, isId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isArray
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isArray(anytype value)
        #{
        #    boolean ret = AnytypeUtil::isClass(value, classnum(Array));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isClass
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isClass(anytype value, int potentialAncestorId = 0)
        #{
        #    boolean ret = AnytypeUtil::isType(Types::Class, potentialAncestorId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isCollection
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true для значения, для которого можно получить enumerator
        #public static boolean isCollection(anytype value)
        #{
        #    return EnumeratorUtil::isEnumerable(value);
        #}
      ENDSOURCE
      SOURCE #isContainer
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isContainer(anytype value)
        #{
        #    boolean ret = typeof(value) == Types::Container;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// нормально работает с контейнерами, классами, CLR и прочими сложными объектами.
        #public static boolean isEmpty(anytype value)
        #{
        #    if (value)
        #    {
        #        return false;
        #    }
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #isEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isEnum(anytype value, enumId enumId = 0)
        #{
        #    return AnytypeUtil::isType(value, Types::Enum, enumId);
        #}
      ENDSOURCE
      SOURCE #isEnumerable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true для значения, для которого можно получить enumerator
        #public static boolean isEnumerable(anytype value)
        #{
        #    return EnumeratorUtil::isEnumerable(value);
        #}
      ENDSOURCE
      SOURCE #isEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true для значения, для которого можно получить enumerator
        #public static boolean isEnumerator(anytype value)
        #{
        #    return EnumeratorUtil::isEnumerator(value);
        #}
      ENDSOURCE
      SOURCE #isImplements
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isImplements(anytype value, int interfaceId = 0)
        #{
        #    boolean ret;
        #    classId classId;
        #
        #    if( interfaceId && typeof(value) == Types::Class )
        #    {
        #        classId = classidget(value);
        #        ret = new SysDictClass(classId).isImplementing(interfaceId);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isInt
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isInt(anytype value)
        #{
        #    boolean ret = typeof(value) == Types::Integer;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isInt64
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isInt64(anytype value)
        #{
        #    boolean ret = typeof(value) == Types::Int64;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isInTypes
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isInTypes(anytype value, container typeCon)
        #{
        #    Types type = AnytypeUtil::type(value);
        #
        #    if (confind(typeCon, type))
        #    {
        #        return true;
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #isList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isList(anytype value)
        #{
        #    boolean ret = AnytypeUtil::isClass(value, classnum(List));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isMap
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isMap(anytype value)
        #{
        #    boolean ret = AnytypeUtil::isClass(value, classnum(Map));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isNotEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// нормально работает с контейнерами, классами, CLR и прочими сложными объектами.
        #public static boolean isNotEmpty(anytype value)
        #{
        #    if (value)
        #    {
        #        return true;
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #isPrimitive
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isPrimitive(anytype value)
        #{
        #    Types type = typeof(value);
        #
        #    switch( type )
        #    {
        #        case Types::String:
        #        case Types::Integer:
        #        case Types::Real:
        #        case Types::Date:
        #        case Types::Enum:
        #        case Types::UtcDateTime:
        #        case Types::RString:
        #        case Types::VarString:
        #        case Types::UserType:
        #        case Types::Guid:
        #        case Types::Int64:
        #        case Types::Time:
        #            return true;
        #
        #        case Types::Container:
        #        case Types::Record:
        #        case Types::AnyType:
        #        case Types::BLOB:
        #        case Types::Class:
        #        case Types::void:
        #            return false;
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #isRecId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isRecId(anytype value)
        #{
        #    boolean ret = typeof(value) == Types::Int64;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isRecord(anytype value, int potentialAncestorId = 0)
        #{
        #    boolean ret = AnytypeUtil::isType(value, Types::Record, potentialAncestorId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isRecVersion
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isRecVersion(anytype value)
        #{
        #    boolean ret = typeof(value) == Types::Integer;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isSet(anytype value)
        #{
        #    boolean ret = AnytypeUtil::isClass(classnum(Set));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isString(anytype value)
        #{
        #    boolean ret = AnytypeUtil::isInTypes(value, AnytypeUtil::stringTypes());
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isType(anytype value, Types type, int potentialAncestorId = 0)
        #{
        #    Types t = typeof(value);
        #
        #    if( potentialAncestorId )
        #    {
        #        switch( t )
        #        {
        #            case Types::Class:
        #                return (type == t) && SysDictClass::is(value, potentialAncestorId);
        #
        #            case Types::Record:
        #                return (type == t) && SysDictTable::is(value, potentialAncestorId);
        #
        #            case Types::Integer:
        #                switch( type )
        #                {
        #                    case Types::Class:
        #                        return SysDictClass::isEqualOrSuperclass(value, potentialAncestorId);
        #
        #                    case Types::Record:
        #                        return SysDictTable::is(SysRecord::makeRecord(value), potentialAncestorId);
        #
        #                    case Types::UserType:
        #                        return SysDictType::isEqualOrExtending(value, potentialAncestorId);
        #
        #                    case Types::Enum:
        #                        return (potentialAncestorId == new DictType(value).enumId());
        #                }
        #                break;
        #
        #            case Types::Enum:
        #                return (type == t) && potentialAncestorId == DictEnum::value2id(value); // TODO равен ли? может ли быть enum от enum?
        #        }
        #    }
        #
        #    if( type == t )
        #    {
        #        return true;
        #    }
        #    else if( confind(AnytypeUtil::stringTypes(),t) )
        #    {
        #        // не строгое совпадение строковых типов поскольку все строки можно присвоить друг другу
        #        return true;
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #nullValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype nullValue(Types type)
        #{
        #    return nullValueBaseType(type);
        #}
      ENDSOURCE
      SOURCE #stringTypes
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container stringTypes()
        #{
        #    return [Types::String, Types::RString, Types::VarString];
        #}
      ENDSOURCE
      SOURCE #tableId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int tableId(anytype value)
        #{
        #    int id = 0;
        #
        #    if( typeof(value) == Types::Record )
        #    {
        #        id = AnytypeUtil::asRecord(value).TableId;
        #    }
        #
        #    return id;
        #}
      ENDSOURCE
      SOURCE #toBuf
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// преобразовать value в соответствующий буфер, насколько это возможно
        #public static Common toBuf(anytype value)
        #{
        #    Common buf;
        #
        #    switch(typeof(value))
        #    {
        #        case Types::Record:
        #            buf = value;
        #            break;
        #
        #        case Types::Integer:
        #            buf = SysRecord::makeRecord(value);
        #            break;
        #
        #        case Types::Int64:
        #            if( value > 0 )
        #            {
        #                buf = SysRecord::makeRecord(int642int(value, true));
        #            }
        #            break;
        #
        #        case Types::String:
        #        case Types::RString:
        #        case Types::VarString:
        #            if( value )
        #            {
        #                buf = SysRecord::makeRecord(tablename2id(value));
        #            }
        #            break;
        #    }
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #toNullValue
        #public static anytype toNullValue(anytype value)
        #{
        #    return nullValueBaseType(value);
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str toString(anytype value, str sep = ',')
        #{
        #    // поскольку этот метод может вызваться в отладчике еще до инициализации,
        #    // то в этом методе нельзя использовать методы класса и нельзя бросать исключение
        #    // и ни в коем случае не бросаем исключение на неинициализированные значения
        #    Types type = typeof(value);
        #    str   ret;
        #    Object obj;
        #
        #    switch (type)
        #    {
        #        case Types::String:
        #        case Types::RString:
        #        case Types::VarString:
        #            ret = value;
        #            break;
        #
        #        case Types::Container:
        #            ret = ConUtil::toStr(value, sep);
        #            break;
        #
        #        case Types::AnyType:
        #            ret = '';
        #            break;
        #
        #        case Types::Record:
        #            ret = RecordUtil::getValuesInfo(value);
        #
        #        case Types::Class:
        #            if( value )
        #            {
        #                obj = value;
        #                ret = obj.toString();
        #            }
        #            else
        #            {
        #                ret = 'null';
        #            }
        #            break;
        #
        #        case Types::BLOB:
        #        case Types::void:
        #            ret = strfmt('%1', type); // не бросаем исключение! throw error(Error::unsupportedEnum(funcname(), type));
        #            break;
        #
        #        default:
        #            ret = strfmt('%1', value);
        #            break;
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #type
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Types type(anytype value)
        #{
        #    Types type = typeof(value);
        #
        #    return type;
        #}
      ENDSOURCE
      SOURCE #undefined
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype undefined()
        #{
        #    anytype undefined;
        #
        #    return undefined;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: ArgsUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #ArgsUtil
    PROPERTIES
      Name                #ArgsUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class ArgsUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #assertCallerFormName
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertCallerFormName(Args args, FormName formName, str _funcname = '')
        #{
        #    if( ArgsUtil::callerFormName(args) == formName)
        #        return;
        #
        #    throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertCallerType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertCallerType(Args args, classId potentialAncestorClassId, str _funcname = '')
        #{
        #    if( ArgsUtil::checkCallerType(args, potentialAncestorClassId) )
        #        return;
        #
        #    throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertEnum(Args args, Anytype enum, str _funcname = '')
        #{
        #    if ( ArgsUtil::checkEnum(args, enum) )
        #        return;
        #
        #    throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertEnumType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertEnumType(Args args, EnumId enumId, str _funcname = '')
        #{
        #    if( ArgsUtil::checkEnumType(args, enumId) )
        #        return;
        #
        #    throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertFormDatasource
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertFormDatasource(Args args, str _funcname = '')
        #{
        #    if (args && args.dataset() && args.record().isFormDataSource())
        #        return;
        #
        #    throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertParm
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertParm(Args args, str matchPattern = '.', str _funcname = '')
        #{
        #    if ( ArgsUtil::checkParm(args, matchPattern) )
        #        return;
        #
        #    throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertRecord(Args args, TableId tableId = 0, str _funcname = '')
        #{
        #    if ( ArgsUtil::checkRecord(args, tableId) )
        #        return;
        #
        #    throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertRecordExists
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertRecordExists(Args args, TableId tableId = 0, str _funcname = '')
        #{
        #    if ( ArgsUtil::checkRecordExists(args, tableId) )
        #        return;
        #
        #    throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #assertRecordTableIds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static void assertRecordTableIds(Args args, container tableIds, str _funcname = '')
        #{
        #    if ( ArgsUtil::checkRecordTableIds(args, tableIds) )
        #        return;
        #
        #    throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
        #}
      ENDSOURCE
      SOURCE #caller
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Object caller(Args args, classId potentialAncestorClassId)
        #{
        #    Object ret = args ? SysDictClass::as(args.caller(), potentialAncestorClassId) : null;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #callerFormName
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str callerFormName(Args args)
        #{
        #    FormRun formRun = ArgsUtil::caller(args, classnum(FormRun));
        #    str formName = formRun ? formRun.form().name() : '';
        #
        #    return formName;
        #}
      ENDSOURCE
      SOURCE #checkCallerType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean checkCallerType(Args args, classId potentialAncestorClassId)
        #{
        #    boolean checkOk = args && SysDictClass::is(args.caller(), potentialAncestorClassId);
        #    ;
        #
        #    return checkOk;
        #}
      ENDSOURCE
      SOURCE #checkEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean checkEnum(Args args, Anytype enum)
        #{
        #    boolean checkOk;
        #
        #    if( args && typeof(enum) == types::Enum )
        #    {
        #        checkOk = (enum == args.parmEnum());
        #    }
        #
        #    return checkOk;
        #}
        #
      ENDSOURCE
      SOURCE #checkEnumType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean checkEnumType(Args args, EnumId enumId)
        #{
        #    boolean checkOk = (args && args.parmEnumType() == enumId);
        #
        #    return checkOk;
        #}
      ENDSOURCE
      SOURCE #checkParm
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// параметр parm содержит паттерн.
        #// возвращает true, если parm-строка соответствует regexp-выражению в matchPattern (метод использует функцию match для проверки)
        #// если не указать matchPattern, то возвращает true, если parm не пустой (содержит хотя бы один символ)
        #// возвращает true, если matchPattern является пустой строкой
        #public static boolean checkParm(Args args, str matchPattern = '.')
        #{
        #    boolean checkOk = (args && match(matchPattern, args.parm()));
        #
        #    return checkOk;
        #}
        #
      ENDSOURCE
      SOURCE #checkRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// запись из указанной таблицы (или любой таблицы для tableId = 0)
        #public static boolean checkRecord(Args args, TableId tableId = 0)
        #{
        #    boolean checkOk = (args != null);
        #    int     dataset;
        #
        #    if( checkOk )
        #    {
        #        dataset = args.dataset();
        #        checkOk = (dataset != 0) && (tableId == 0 || dataset == tableId);
        #    }
        #
        #    return checkOk;
        #}
      ENDSOURCE
      SOURCE #checkRecordExists
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// запись существует (recId != 0) и запись из указанной таблицы (или любой таблицы для tableId = 0)
        #public static boolean checkRecordExists(Args args, TableId tableId = 0)
        #{
        #    boolean checkOk = (ArgsUtil::checkRecord(args, tableId) && args.record() && args.record().RecId != 0);
        #;
        #
        #    return checkOk;
        #}
      ENDSOURCE
      SOURCE #checkRecordTableIds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true, если:
        #// * контейнер содержит tableId, совпадающий с tableId таблицы
        #// * контейнер содержит 0
        #// * контейнер пустой
        #// возвращает false в остальных случаях
        #public static boolean checkRecordTableIds(Args args, container tableIds)
        #{
        #    boolean checkOk = (args != null);
        #    int     dataset;
        #
        #    if( checkOk && tableIds )
        #    {
        #        dataset = args.dataset();
        #        checkOk = confind(tableIds, dataset) || confind(tableIds, 0);
        #    }
        #
        #    return checkOk;
        #}
      ENDSOURCE
      SOURCE #copyArgs
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Args copyArgs(Args fromArgs, Args toArgs = null)
        #{
        #    if (fromArgs)
        #    {
        #        if (!toArgs)
        #        {
        #            toArgs = new Args();
        #        }
        #
        #        SysArgs::copyArgs(fromArgs, toArgs);
        #    }
        #
        #    return toArgs;
        #}
        #
      ENDSOURCE
      SOURCE #enum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// убедиться что в аргументах enum указанного типа и
        #// вернуть значение этого enum из аргументов
        #// бросает исключение, если enum не указанного типа
        #public static int enum(Args args, EnumId enumId, str _funcname = '')
        #{;
        #    ArgsUtil::assertEnumType(args, enumId, _funcname);
        #
        #    return args.parmEnum();
        #}
      ENDSOURCE
      SOURCE #enumDefault
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// тоже самое, что и ArgsUtil::getEnum()
        #// но если в аргументах не нужный enum не выдает ошибку, а возвращает дефолтное значение.
        #public static int enumDefault(Args args, anytype defaultEnumValue)
        #{
        #    if( typeof(defaultEnumValue) == Types::Enum )
        #    {
        #        if( ArgsUtil::checkEnumType(args, AnytypeUtil::enumId(defaultEnumValue)) )
        #        {
        #            return args.parmEnum();
        #        }
        #
        #        return defaultEnumValue;
        #    }
        #
        #    throw error::wrongUseOfFunction(funcname());
        #}
      ENDSOURCE
      SOURCE #markedList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container markedList(Args args, TableId tableId = 0, str _funcname = '')
        #{
        #    List            ret = new List(Types::Int64);
        #    FormDataSource  fds;
        #    common          record;
        #
        #    if( ArgsUtil::checkRecord(args, tableId) )
        #    {
        #        fds = args.record().dataSource();
        #        if( fds && fds.anyMarked() )
        #        {
        #            // marked records
        #            for( record = fds.getFirst(true) ; record ; record = fds.getNext() )
        #            {
        #                ret.addEnd(record.RecId);
        #            }
        #        }
        #
        #        if( ret.elements() == 0 )
        #        {
        #            // or a record itself
        #            record = ArgsUtil::record(args, tableId, _funcname);
        #            ret.addEnd(record.RecId);
        #        }
        #    }
        #
        #    return ret.pack();
        #}
      ENDSOURCE
      SOURCE #markedRecIds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container markedRecIds(Args args, TableId tableId = 0, str _funcname = '')
        #{
        #    container       ret;
        #    FormDataSource  fds;
        #    common          record;
        #
        #    if( ArgsUtil::checkRecord(args, tableId) )
        #    {
        #        fds = args.record().dataSource();
        #        if( fds && fds.anyMarked() )
        #        {
        #            // marked records
        #            for( record = fds.getFirst(true) ; record ; record = fds.getNext() )
        #            {
        #                ret += record.RecId;
        #            }
        #        }
        #
        #        if( !ret )
        #        {
        #            // or a record itself
        #            record = ArgsUtil::record(args, tableId, _funcname);
        #            ret += record.RecId;
        #        }
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #markedSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container markedSet(Args args, TableId tableId = 0, str _funcname = '')
        #{
        #    Set             ret = new Set(Types::Int64);
        #    FormDataSource  fds;
        #    common          record;
        #
        #    if( ArgsUtil::checkRecord(args, tableId) )
        #    {
        #        fds = args.record().dataSource();
        #        if( fds && fds.anyMarked() )
        #        {
        #            // marked records
        #            for( record = fds.getFirst(true) ; record ; record = fds.getNext() )
        #            {
        #                ret.add(record.RecId);
        #            }
        #        }
        #
        #        if( ret.elements() == 0 )
        #        {
        #            // or a record itself
        #            record = ArgsUtil::record(args, tableId, _funcname);
        #            ret.add(record.RecId);
        #        }
        #    }
        #
        #    return ret.pack();
        #}
      ENDSOURCE
      SOURCE #record
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common record(Args args, TableId tableId = 0, str _funcname = '')
        #{;
        #    ArgsUtil::assertRecord(args, tableId, _funcname);
        #
        #    return args.record();
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: ConUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #ConUtil
    PROPERTIES
      Name                #ConUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class ConUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #compact
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает новый контейнер, в котором удалены все пустые элементы
        #public static container compact(container con)
        #{
        #    ConEnumerator ce = ConEnumerator::construct(con);
        #    container ret;
        #
        #    while( ce && ce.moveNext() )
        #    {
        #        if( ce.current() )
        #        {
        #            ret += ce.current();
        #        }
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #distinct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает новый контейнер, в котором присутствуют только уникальные объекты (дубли удалены)
        #public static container distinct(container con)
        #{
        #    ConEnumerator ce = ConEnumerator::construct(con);
        #    container ret;
        #
        #    while( ce && ce.moveNext() )
        #    {
        #        if( !confind(ret, ce.current()) )
        #        {
        #            ret += ce.current();
        #        }
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #fromArray
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container fromArray(Array array)
        #{
        #    container ret;
        #    int i;
        #
        #    for( i=1; array && i<=array.lastIndex(); ++i )
        #    {
        #        ret += array.value(i);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #fromBlob
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container fromBlob(container blob)
        #{
        #    return ContainerClass::blob2Container(blob);
        #}
      ENDSOURCE
      SOURCE #fromList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container fromList(List list)
        #{
        #    container ret;
        #    ListEnumerator le = list ? list.getEnumerator() : null;
        #
        #    while( le && le.moveNext() )
        #    {
        #        ret += le.current();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #fromMap
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container fromMap(Map map)
        #{
        #    container ret;
        #    MapEnumerator me = map ? map.getEnumerator() : null;
        #
        #    while( me && me.moveNext() )
        #    {
        #        ret += [me.currentKey(), me.currentValue()];
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #fromSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container fromSet(Set set)
        #{
        #    container ret;
        #    SetEnumerator se = set ? set.getEnumerator() : null;
        #
        #    while( se && se.moveNext() )
        #    {
        #        ret += se.current();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #fromXml
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container fromXml(XmlNode node)
        #{
        #    return containerFromXMLNode(node);
        #}
      ENDSOURCE
      SOURCE #in
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean in(anytype value, container con)
        #{
        #    boolean ret = (confind(con, value) != 0);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #match
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// проверяет есть ли вхожение паттерна в какую-нибудь строку внутри контейнера
        #// проверку выполняет рекурсивно
        #public static boolean match(
        #    str         pattern,
        #    container   con)
        #{
        #    int     len     = conlen(con);
        #    int     i;
        #    Any     value;
        #
        #    for( i=1; i<=len; ++i )
        #    {
        #        value = Any::conpeek(con, i);
        #
        #        switch( value.type() )
        #        {
        #            case Types::String:
        #            case Types::RString:
        #            case Types::VarString:
        #                if( match(pattern, value.asString()) )
        #                    return true;
        #                break;
        #
        #            case Types::Container:
        #                if( ConUtil::match(pattern, value.asContainer()) )
        #                    return true;
        #                break;
        #
        #            case Types::Record:
        #                if( SysRecord::match(pattern, value.asRecord()) )
        #                    return true;
        #                break;
        #
        #            default:
        #                break;
        #        }
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #peekAs
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype peekAs(container con, int idx, anytype asValue)
        #{
        #    anytype ret = AnytypeUtil::as(conpeek(con, idx), asValue);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsContainer
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container peekAsContainer(container con, int idx)
        #{
        #    container ret = AnytypeUtil::asContainer(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsDate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Date peekAsDate(container con, int idx)
        #{
        #    Date ret = AnytypeUtil::asDate(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsDateTime
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static UtcDateTime peekAsDateTime(container con, int idx)
        #{
        #    utcdatetime ret = AnytypeUtil::asDateTime(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype peekAsEnum(container con, int idx, enumId enumId = 0)
        #{
        #    anytype ret = AnytypeUtil::asEnum(conpeek(con, idx), enumId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsGUID
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static GUID peekAsGUID(container con, int idx)
        #{
        #    GUID ret = AnytypeUtil::asGUID(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsInt
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int peekAsInt(container con, int idx)
        #{
        #    int ret = AnytypeUtil::asInt(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsInt64
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Int64 peekAsInt64(container con, int idx)
        #{
        #    Int64 ret = AnytypeUtil::asInt64(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsReal
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Real peekAsReal(container con, int idx)
        #{
        #    Real ret = AnytypeUtil::asInt(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsRecId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static int64 peekAsRecId(container con, int idx)
        #{
        #    Int64 ret = AnytypeUtil::asRecId(conpeek(con, idx));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common peekAsRecord(container con, int idx, TableId tableId = 0)
        #{
        #    Common ret = AnytypeUtil::asRecord(conpeek(con, idx), tableId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #peekAsType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype peekAsType(container con, int idx, Types type, int potentialAncestorId = 0)
        #{
        #    anytype ret = AnytypeUtil::asType(conpeek(con, idx), type, potentialAncestorId);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #remove
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container remove(container con, anytype value)
        #{
        #    int i = confind(con, value);
        #
        #    if( i )
        #    {
        #        con = condel(con, i, 1);
        #    }
        #
        #    return con;
        #}
      ENDSOURCE
      SOURCE #removeAll
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container removeAll(container con, anytype value)
        #{
        #    int i;
        #
        #    do
        #    {
        #        i = confind(con, value);
        #        if( i )
        #        {
        #            con = condel(con, i, 1);
        #        }
        #    }
        #    while( i );
        #
        #    return con;
        #}
      ENDSOURCE
      SOURCE #toArray
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Array toArray(container con, Array array = null)
        #{
        #    Any any;
        #    int i;
        #    int len = conlen(con);
        #
        #    for( i=1; i<=len; ++i )
        #    {
        #        any = Any::conpeek(con, i);
        #
        #        if( !array )
        #        {
        #            array = new Array(any.type());
        #        }
        #
        #        if( any.type() == array.typeId() )
        #        {
        #            array.value(i, any.value());
        #        }
        #    }
        #
        #    return array;
        #}
      ENDSOURCE
      SOURCE #toBlob
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container toBlob(container con)
        #{
        #    ContainerClass containerClass = new ContainerClass(con);
        #    return containerClass.toBlob();
        #}
      ENDSOURCE
      SOURCE #toList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static List toList(container con, List list = null)
        #{
        #    Any any;
        #    int i;
        #    int len = conlen(con);
        #
        #    for( i=1; i<=len; ++i )
        #    {
        #        any = Any::conpeek(con, i);
        #
        #        if( !list )
        #        {
        #            list = new List(any.type());
        #        }
        #
        #        if( any.type() == list.typeId() )
        #        {
        #            list.addEnd(any.value());
        #        }
        #    }
        #
        #    return list;
        #}
      ENDSOURCE
      SOURCE #toSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Set toSet(container con, Set set = null)
        #{
        #    Any any;
        #    int i;
        #    int len = conlen(con);
        #
        #    for( i=1; i<=len; ++i )
        #    {
        #        any = Any::conpeek(con, i);
        #
        #        if( !set )
        #        {
        #            set = new Set(any.type());
        #        }
        #
        #        if( any.type() == set.typeId() )
        #        {
        #            set.add(conpeek(con, i));
        #        }
        #    }
        #
        #    return set;
        #}
      ENDSOURCE
      SOURCE #toStr
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str toStr(container con, str sep = ',')
        #{
        #    str ret = TextBufferUtil::appendContainer(null, con, sep).getText();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toXmlString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str toXmlString(container con, int indent = 0)
        #{
        #    return XMLString(con, types::Container, indent);
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: ListUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #ListUtil
    PROPERTIES
      Name                #ListUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class ListUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #newBy
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// создать list для хранения значений, тип которых совпадает с типом переменной value
        #public static List newBy(anytype value)
        #{
        #    Types type = typeof(value);
        #    List ret = new List(type);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container toCon(List list)
        #{
        #    container ret;
        #    ListEnumerator le = list.getEnumerator();
        #
        #    while( le.moveNext() )
        #    {
        #        ret += le.current();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Set toSet(List list)
        #{
        #    Set set;
        #    ListEnumerator le;
        #
        #    if (list)
        #    {
        #        set = new Set(list.typeId());
        #
        #        le = list.getEnumerator();
        #        while (le.moveNext())
        #        {
        #            set.add(le.current());
        #        }
        #    }
        #
        #    return set;
        #}
      ENDSOURCE
      SOURCE #toStr
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str toStr(List list, str sep = ',')
        #{
        #    ListEnumerator le = list ? list.getEnumerator() : null;
        #    str ret = TextBufferUtil::appendEnumerator(null, le, sep).getText();
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SetUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SetUtil
    PROPERTIES
      Name                #SetUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class SetUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #newBy
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// создать set для хранения значений, тип которых совпадает с типом переменной value
        #public static Set newBy(anytype value)
        #{
        #    Types type = typeof(value);
        #    Set ret = new Set(type);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container toCon(Set set)
        #{
        #    container ret;
        #    SetEnumerator se = set.getEnumerator();
        #
        #    while( se.moveNext() )
        #    {
        #        ret += se.current();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static List toList(Set set)
        #{
        #    List list;
        #    SetEnumerator se;
        #
        #    if (set)
        #    {
        #        list = new List(set.typeId());
        #
        #        se = set.getEnumerator();
        #        while (se.moveNext())
        #        {
        #            list.addEnd(se.current());
        #        }
        #    }
        #
        #    return list;
        #}
      ENDSOURCE
      SOURCE #toStr
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str toStr(Set set, str sep = ',')
        #{
        #    SetEnumerator se = set ? set.getEnumerator() : null;
        #    str ret = TextBufferUtil::appendEnumerator(null, se, sep).getText();
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: ClrTypeUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #ClrTypeUtil
    PROPERTIES
      Name                #ClrTypeUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class ClrTypeUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #asInstance
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static System.Object asInstance(System.Object obj, System.Object baseObj)
        #{
        #    if( ClrTypeUtil::isInstance(obj, baseObj) )
        #    {
        #        return obj;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asInstanceOrBase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static System.Object asInstanceOrBase(System.Object obj, System.Object baseObj)
        #{
        #    if( ClrTypeUtil::isInstance(obj, baseObj) )
        #    {
        #        return obj;
        #    }
        #
        #    return baseObj;
        #}
        #
      ENDSOURCE
      SOURCE #isInstance
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isInstance(System.Object obj, System.Object baseObj)
        #{
        #    anytype perm = new InteropPermission(Interopkind::ClrInterop).assert();
        #
        #    System.Type baseType = baseObj ? baseObj.GetType() : null;
        #    boolean     ret      = baseType && obj ? baseType.IsInstanceOfType(obj) : false;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #name
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str name(System.Object obj)
        #{
        #    anytype perm = new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    System.Type type = obj ? obj.GetType() : null;
        #    str typeName = type ? type.get_Name() : '';
        #
        #    return typeName;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: EnumeratorUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #EnumeratorUtil
    PROPERTIES
      Name                #EnumeratorUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class EnumeratorUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #asEnumerable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asEnumerable(anytype value)
        #{
        #    if( EnumeratorUtil::isEnumerable(value) )
        #    {
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #asEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype asEnumerator(anytype value)
        #{
        #    if( EnumeratorUtil::isEnumerator(value) )
        #    {
        #        return value;
        #    }
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #getEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает объект, у которого можно вызвать метод moveNext или Null
        #public static Enumerator getEnumerator(anytype value)
        #{
        #    Object obj;
        #
        #    if( EnumeratorUtil::isEnumerable(value) )
        #    {
        #        switch( typeof(value) )
        #        {
        #            case Types::Container:
        #                return ConEnumerator::construct(value);
        #
        #            case Types::Class:
        #                if( value )
        #                {
        #                    obj = value; // проверка isEnumerable гарантирует, что метод getEnumerator существует и определен правильно
        #                    return obj.getEnumerator();
        #                }
        #                else
        #                {
        #                    return null;
        #                }
        #                break;
        #        }
        #    }
        #
        #    if( value )
        #    {
        #        return OneValueEnumerator::construct(value);
        #    }
        #
        #    return null; // можно вручную создать enumerator на коллекцию из одного null, если кто-то захочет
        #}
      ENDSOURCE
      SOURCE #isEnumerable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true для значения, для которого можно получить enumerator
        #public static boolean isEnumerable(anytype value)
        #{
        #    Types type = typeof(value);
        #    SysDictMethod sdm;
        #
        #    switch( type )
        #    {
        #        case Types::Container:
        #            return true;
        #
        #        case Types::Class:
        #            if( value )
        #            {
        #                if( AnytypeUtil::isList(value) )
        #                    return true;
        #
        #                if( AnytypeUtil::isMap(value) )
        #                    return true;
        #
        #                if( AnytypeUtil::isSet(value) )
        #                    return true;
        #
        #                if( AnytypeUtil::isClass(value, classnum(Enumerable)) )
        #                    return true;
        #
        #                // утиная типизация: есть метод getEnumerator,
        #                // у которого нет параметров и который возвращает объект
        #                sdm = new SysDictMethod(UtilElementType::ClassInstanceMethod, classidget(value), identifierstr(getEnumerator));
        #                if( sdm &&
        #                    sdm.parameterCnt() == 0 &&
        #                    sdm.returnType() == Types::Class &&
        #                    SysDictClass::construct(sdm.returnId()).isImplementing(classnum(Enumerator)) )
        #                {
        #                    return true;
        #                }
        #            }
        #            break;
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #isEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true для значения, для которого можно получить enumerator
        #public static boolean isEnumerator(anytype value)
        #{
        #    return AnytypeUtil::isImplements(value, classnum(Enumerator));
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: ConEnumerator unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #ConEnumerator
    PROPERTIES
      Name                #ConEnumerator
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public class ConEnumerator implements Enumerator
        #{
        #    int         idx;
        #    int         len;
        #    container   con;
        #}
      ENDSOURCE
      SOURCE #current
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public anytype current()
        #{
        #    // никаких проверок:
        #    // если индекс выходит за границы контейнера, то conPeek возвратит целый 0
        #    return conPeek(con, idx);
        #}
      ENDSOURCE
      SOURCE #definitionString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str definitionString()
        #{
        #    return 'container enumerator';
        #}
      ENDSOURCE
      SOURCE #init
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// метод публичный, чтобы объект данного класса можно было создавать из classFactory и инициализировать из всевозможных unpack
        #// не вызывайте этот метод напрямую
        #public void init(container _con)
        #{
        #    con = _con;
        #    len = conlen(con);
        #}
        #
      ENDSOURCE
      SOURCE #moveNext
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean moveNext()
        #{
        #    idx++;
        #
        #    return (idx <= len);
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void new(container _con = connull())
        #{
        #    this.init(con);
        #}
        #
      ENDSOURCE
      SOURCE #reset
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void reset()
        #{
        #    idx = 0;
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toString()
        #{
        #    // поскольку этот метод может вызваться в отладчике еще до инициализации,
        #    // то в этом методе нельзя использовать методы класса и нельзя бросать исключение
        #    // и ни в коем случае не бросаем исключение на неинициализированные значения
        #    return AnytypeUtil::toString(this.current());
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static server ConEnumerator construct(container con)
        #{
        #    ConEnumerator conEnumerator = new ConEnumerator(con);
        #
        #    return conEnumerator;
        #}
        #
      ENDSOURCE
      SOURCE #description
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static ClassDescription description()
        #{
        #    return "An enumerator to traverse a container.";
        #}
        #
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: OneValueEnumerator unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #OneValueEnumerator
    PROPERTIES
      Name                #OneValueEnumerator
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// enumerator по ОДНОМУ значению произвольного типа
        #class OneValueEnumerator implements Enumerator
        #{
        #    anytype currentValue;
        #    int pos; // 0: before, 1: inside, >1: after. Inside if (pos == 1)
        #}
      ENDSOURCE
      SOURCE #current
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public anytype current()
        #{
        #    if( pos == 1 )
        #    {
        #        return currentValue;
        #    }
        #
        #    return AnytypeUtil::undefined();
        #}
      ENDSOURCE
      SOURCE #definitionString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str definitionString()
        #{
        #    return strfmt('anytype[%1] enumerator', typeof(currentValue));
        #}
      ENDSOURCE
      SOURCE #init
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// метод публичный, чтобы объект данного класса можно было создавать из classFactory и инициализировать из всевозможных unpack
        #// повторный вызов этого метода не приведет к изменению типа переменной currentValue. И не должен приводить.
        #// не вызывайте этот метод напрямую.
        #public void init(anytype value)
        #{
        #    currentValue = value;
        #}
        #
      ENDSOURCE
      SOURCE #moveNext
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean moveNext()
        #{
        #    pos++;
        #
        #    return (pos == 1);
        #}
      ENDSOURCE
      SOURCE #new
        #public void new(anytype value = 0)
        #{
        #    this.init(value);
        #}
      ENDSOURCE
      SOURCE #reset
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void reset()
        #{
        #    pos = 0;
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toString()
        #{
        #    if( pos > 1 )
        #    {
        #        return '(end)';
        #    }
        #    else if( pos < 1 )
        #    {
        #        return '(before)';
        #    }
        #
        #    return strfmt('[%1]', currentValue);
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Enumerator construct(anytype value)
        #{
        #    return new OneValueEnumerator(value);
        #}
        #
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: Error unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #Error
    PROPERTIES
      Name                #Error
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #unsupportedEmptyString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// в качестве описания можно использовать название переменной
        #public static LabelType unsupportedEmptyString(str _funcName, anytype _description = '')
        #{
        #    LabelType ret = strfmt("unsupported empty string in %1 %2", _funcName, _description);
        #
        #    return ret;
        #    }
      ENDSOURCE
      SOURCE #unsupportedEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static LabelType unsupportedEnum(str _funcName, anytype _enumValue = '')
        #{
        #    LabelType ret = strfmt("Unsupported enum <%1> in %2", _enumValue, _funcName);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #unsupportedNull
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// в качестве описания можно использовать название переменной
        #public static LabelType unsupportedNull(str _funcName, anytype _description = '')
        #{
        #    LabelType ret = strfmt("unsupported Null in %1 %2", _funcName, _description);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #unsupportedType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static LabelType unsupportedType(str _funcName, anytype type = '')
        #{
        #    LabelType ret = strfmt("unsupported Type %1 - %2", type, _funcName);
        #
        #    ret += TextUtil::callStack();
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysQuery unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysQuery
    PROPERTIES
      Name                #SysQuery
      Extends             #Query
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #addRangesFromCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static void addRangesFromCon(QueryBuildDataSource qbds, FieldId fieldId, container con)
        #{
        #    ConEnumerator ce;
        #
        #    if( qbds && fieldId && con )
        #    {
        #        ce = ConEnumerator::construct(con);
        #        while (ce && ce.moveNext())
        #        {
        #            qbds.addRange(fieldId).value(queryValue(ce.current()));
        #        }
        #    }
        #}
      ENDSOURCE
      SOURCE #addRangesFromEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static void addRangesFromEnumerator(QueryBuildDataSource qbds, FieldId fieldId, Enumerator e)
        #{
        #    if( qbds && fieldId )
        #    {
        #        while (e && e.moveNext())
        #        {
        #            qbds.addRange(fieldId).value(queryValue(e.current()));
        #        }
        #    }
        #}
      ENDSOURCE
      SOURCE #addRangesFromList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static void addRangesFromList(QueryBuildDataSource qbds, FieldId fieldId, List list)
        #{
        #    ListEnumerator le;
        #
        #    if( qbds && fieldId && list )
        #    {
        #        le = list.getEnumerator();
        #        while (le && le.moveNext())
        #        {
        #            qbds.addRange(fieldId).value(queryValue(le.current()));
        #        }
        #    }
        #}
      ENDSOURCE
      SOURCE #addRangesFromSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static void addRangesFromSet(QueryBuildDataSource qbds, FieldId fieldId, Set set)
        #{
        #    SetEnumerator se;
        #
        #    if( qbds && fieldId && set )
        #    {
        #        se = set.getEnumerator();
        #        while (se && se.moveNext())
        #        {
        #            qbds.addRange(fieldId).value(queryValue(se.current()));
        #        }
        #    }
        #}
      ENDSOURCE
      SOURCE #findGroupByField
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static QueryGroupByField findGroupByField(
        #    Query                   _query,
        #    QueryBuildDataSource    _qbds,
        #    fieldId                 _fieldId)
        #{
        #    int                 groupFields;
        #    int                 i;
        #    QueryGroupByField   queryGroupByField;
        #
        #    if (_query && _qbds)
        #    {
        #        groupFields = _query.groupByFieldCount();
        #        for (i=1; i <= groupFields; ++i)
        #        {
        #            queryGroupByField = _query.groupByField(i);
        #
        #            if (queryGroupByField
        #                && queryGroupByField.fieldID() == _fieldId
        #                && queryGroupByField.dataSource().table() == _qbds.table())
        #            {
        #                break;
        #            }
        #        }
        #    }
        #
        #    return queryGroupByField;
        #}
      ENDSOURCE
      SOURCE #findOrCreateGroupByField
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static QueryGroupByField findOrCreateGroupByField(Query _query, QueryBuildDataSource _queryBuildDataSource, fieldId _fieldId)
        #{
        #    QueryGroupByField    queryGroupByField;
        #
        #    if (_query && _queryBuildDataSource)
        #    {
        #        queryGroupByField = SysQuery::findGroupByField(_query, _queryBuildDataSource, _fieldId);
        #
        #        if (!queryGroupByField)
        #        {
        #            queryGroupByField = _queryBuildDataSource.addGroupByField(_fieldId);
        #        }
        #    }
        #
        #    return queryGroupByField;
        #}
      ENDSOURCE
      SOURCE #findOrCreateRange
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static QueryBuildRange findOrCreateRange(QueryBuildDataSource _queryBuildDataSource, fieldId _fieldId)
        #{
        #    QueryBuildRange queryBuildRange;
        #
        #    if (_queryBuildDataSource)
        #    {
        #        queryBuildRange = _queryBuildDataSource.findRange(_fieldId);
        #
        #        if (!queryBuildRange && _fieldId)
        #        {
        #            queryBuildRange = _queryBuildDataSource.addRange(_fieldId);
        #        }
        #    }
        #    return queryBuildRange;
        #}
      ENDSOURCE
      SOURCE #findOrCreateRangeValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// использует уже существующий range или добавляет новый
        #// сразу устанавливает value (включая контейнер), выполняет экранирование
        #public static QueryBuildRange findOrCreateRangeValue(
        #    QueryBuildDataSource    _queryBuildDataSource,
        #    fieldId                 _fieldId,
        #    Anytype                 _value)
        #{
        #    QueryBuildRange     queryBuildRange     = SysQuery::findOrCreateRange(_queryBuildDataSource, _fieldId);
        #    str                 strValue;
        #
        #    if (queryBuildRange)
        #    {
        #        strValue = SysQuery::value(_value);
        #        queryBuildRange.value(strValue);
        #    }
        #
        #    return queryBuildRange;
        #}
      ENDSOURCE
      SOURCE #findOrCreateRangeValueNot
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// использует уже существующий range или добавляет новый
        #// сразу устанавливает value (включая контейнер), выполняет экранирование
        #public static QueryBuildRange findOrCreateRangeValueNot(
        #    QueryBuildDataSource    _queryBuildDataSource,
        #    fieldId                 _fieldId,
        #    Anytype                 _value)
        #{
        #    QueryBuildRange     queryBuildRange     = SysQuery::findOrCreateRange(_queryBuildDataSource, _fieldId);
        #    str                 strValue;
        #
        #    if (queryBuildRange)
        #    {
        #        strValue = SysQuery::valueNot(_value);
        #        queryBuildRange.value(strValue);
        #    }
        #
        #    return queryBuildRange;
        #}
      ENDSOURCE
      SOURCE #query
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Query query(
        #    TableId     _tableId,
        #    FieldId     _fieldId    = 0,
        #    Anytype     _value      = null)
        #{
        #    Query                   q       = new Query();
        #    QueryBuildDataSource    qbds    = q.addDataSource(_tableId);
        #
        #    if (_fieldId && qbds && !prmisdefault(_value))
        #    {
        #        SysQuery::findOrCreateRangeValue(qbds, _fieldId, _value);
        #    }
        #
        #    return q;
        #}
      ENDSOURCE
      SOURCE #queryRun
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static QueryRun queryRun(
        #    TableId     _tableId,
        #    FieldId     _fieldId    = 0,
        #    Anytype     _value      = null)
        #{
        #    Query       q   = SysQuery::query(_tableId, _fieldId, _value);
        #    QueryRun    qr  = new QueryRun(q);
        #
        #    return qr;
        #}
      ENDSOURCE
      SOURCE #take
        #// получить из запроса n записей, начиная со startFrom
        #public static QueryRun take(Query q, int n, int startFrom = 1)
        #{
        #    QueryRun qr = new QueryRun(q);
        #
        #    qr = QueryRunUtil::take(qr, n, startFrom);
        #
        #    return qr;
        #}
      ENDSOURCE
      SOURCE #value
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str value(anytype a)
        #{
        #    str quotableStr = ' *",.()\\';
        #    str s;
        #    int t;
        #
        #    str doEscape(str s1)
        #    {
        #        int i = 1;
        #        int pos;
        #        int len = strlen(s1);
        #        str result;
        #        pos = strfind(s1, quotableStr, i, len - i + 1);
        #        while (pos)
        #        {
        #            result += substr(s1, i, pos - i) + '\\' + substr(s1, pos, 1);
        #            i = pos + 1;
        #            if (i<=len)
        #                pos = strfind(s1, quotableStr, i, len - i + 1);
        #            else
        #                pos = 0;
        #        }
        #
        #        if (i == 1)
        #            return s1;
        #
        #        if (i<=len)
        #            result += substr(s1, i, len - i + 1);
        #
        #        return result;
        #    }
        #
        #    t = typeof(a);
        #    switch (t)
        #    {
        #        case Types::Enum:
        #            return enum2Value(a);
        #        case Types::Real:
        #            return num2str(a,0,16,1,0);
        #        case Types::Date:
        #        case Types::UtcDateTime:
        #            return strfmt('\"%1\"',a);  // must be double quotes
        #        case
        #            Types::String,
        #            Types::RString,
        #            Types::VarString:
        #        {
        #            s = a;
        #            if (!s)
        #                return SysQuery::valueEmptyString();
        #            return strfmt('%1',doEscape(s));  // must be double quotes
        #        }
        #        //mazzy 03.12.2019 -->
        #        case Types::Container:
        #            return Any::construct(a).toQueryValue();
        #        case Types::Class:
        #            if (SysDictClass::isEqualOrSuperclass(classidget(a), classnum(Set)) ||
        #                SysDictClass::isEqualOrSuperclass(classidget(a), classnum(List)) ||
        #                SysDictClass::isEqualOrSuperclass(classidget(a), classnum(Array)))
        #            {
        #                return Any::construct(a).toQueryValue();
        #            }
        #       //mazzy 03.12.2019 <--
        #    }
        #    // <GEEU>
        #
        #    // to avoid stack trace on unsupported value 'null'
        #    if (! new DictEnum(enumnum(Types)).value2Symbol(t))
        #        return SysQuery::valueUnlimited();
        #
        #        // </GEEU>
        #    return strfmt('%1',a);
        #}
      ENDSOURCE
      SOURCE #valueNot
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str valueNot(anytype a)
        #{
        #    str ret;
        #    int i;
        #    int len;
        #    container con;
        #    ;
        #
        #    switch(typeof(a))
        #    {
        #        case Types::Container:
        #            len = conlen(a);
        #            for( i=1; i<=len; ++i)
        #            {
        #                con += SysQuery::valueNot(conpeek(a, i));
        #            }
        #            ret = con2str(con);
        #            break;
        #
        #        case Types::Class:
        #            if (SysDictClass::isEqualOrSuperclass(a, classnum(Set)))
        #            {
        #                ret = SysQuery::valueNot(ConUtil::fromArray(a));
        #                break;
        #            }
        #            else if (SysDictClass::isEqualOrSuperclass(a, classnum(List)))
        #            {
        #                ret = SysQuery::valueNot(ConUtil::fromList(a));
        #                break;
        #            }
        #            else if (SysDictClass::isEqualOrSuperclass(a, classnum(Array)))
        #            {
        #                ret = SysQuery::valueNot(ConUtil::fromArray(a));
        #                break;
        #            }
        #            // no break! проваливаемся ниже
        #        default:
        #            ret = strfmt('!%1',SysQuery::value(a));
        #            break;
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysQueryTest unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysQueryTest
    PROPERTIES
      Name                #SysQueryTest
      Extends             #SysTestCase
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class SysQueryTest extends SysTestCase
        #{
        #}
      ENDSOURCE
      SOURCE #testValue_Container
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testValue_Container()
        #{
        #    InventLocation  inventLocation;
        #    container       con         = [InventLocationType::Quarantine, InventLocationType::Standard];
        #    str             range       = SysQuery::value(con);
        #    Query           q           = new Query();
        #    QueryRun        qr;
        #    boolean         found;
        #    ;
        #
        #    q.addDataSource(inventLocation.TableId)
        #        .addRange(fieldnum(InventLocation, InventLocationType))
        #        .value(range);
        #
        #    qr = new QueryRun(q);
        #    while(qr.next())
        #    {
        #        inventLocation = qr.get(inventLocation.TableId);
        #        this.assertNotEqual(0, confind(con, inventLocation.InventLocationType));
        #        found = true;
        #    }
        #
        #    this.assertTrue(found);
        #}
      ENDSOURCE
      SOURCE #testValueNot_Container
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testValueNot_Container()
        #{
        #    InventLocation  inventLocation;
        #    container       con         = [InventLocationType::Quarantine, InventLocationType::Standard];
        #    str             range       = SysQuery::valueNot(con);
        #    Query           q           = new Query();
        #    QueryRun        qr;
        #    boolean         found;
        #    ;
        #
        #    q.addDataSource(inventLocation.TableId)
        #        .addRange(fieldnum(InventLocation, InventLocationType))
        #        .value(range);
        #
        #    qr = new QueryRun(q);
        #    while(qr.next())
        #    {
        #        inventLocation = qr.get(inventLocation.TableId);
        #        this.assertEquals(0, confind(con, inventLocation.InventLocationType));
        #        found = true;
        #    }
        #
        #    this.assertTrue(found);
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: QueryRunUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #QueryRunUtil
    PROPERTIES
      Name                #QueryRunUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class QueryRunUtil
        #{
        #}
      ENDSOURCE
      SOURCE #getByName
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common getByName(QueryRun qr, str dsName)
        #{
        #    Query                   q       = qr && dsName ? qr.query() : null;
        #    QueryBuildDataSource    qbds    = q ? q.dataSourceName(dsName) : null;
        #    Common                  ret;
        #
        #    if( qbds )
        #    {
        #        ret = qr.getNo(qbds.id());
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #take
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// получить из запроса n записей, начиная со startFrom
        #public static QueryRun take(QueryRun qr, int n, int startFrom = 1)
        #{
        #    if( qr && n )
        #    {
        #        qr.enablePositionPaging(true);
        #        qr.addPageRange(startFrom, n);
        #    }
        #
        #    return qr;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: RecordUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #RecordUtil
    PROPERTIES
      Name                #RecordUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class RecordUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #cacheLookup
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static RecordCacheLevel cacheLookup(Common rec)
        #{
        #    RecordCacheLevel ret = RecordUtil::dictTable(rec).cacheLookup();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #clone
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common clone(Common orig, RefTableId expectedTableId = 0)
        #{
        #    Common clone;
        #
        #    if( expectedTableId && expectedTableId != orig.TableId && expectedTableId != RecordUtil::commonTableId() )
        #    {
        #        throw error(strFmt('%1: Не совпадают TableId. Ожидается %2, фактическое значение %3',
        #                        funcname(),
        #                        SysDictTable::construct(expectedTableId).toString(),
        #                        SysDictTable::construct(orig.TableId).toString()
        #        ));
        #    }
        #
        #    clone.data(orig);
        #
        #    return clone;
        #}
      ENDSOURCE
      SOURCE #commonRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common commonRecord()
        #{
        #    Common rec;
        #
        #    return rec;
        #}
      ENDSOURCE
      SOURCE #commonTableId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TableId commonTableId()
        #{
        #    // Common rec;
        #    //
        #    // return rec.TableId;
        #
        #    return 65535;
        #}
      ENDSOURCE
      SOURCE #dictTable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// всегда возвращает not nullable объект. Полезно в выражениях
        #public static DictTable dictTable(Common rec)
        #{
        #    DictTable dt = new DictTable(rec.TableId);
        #
        #    if( !dt )
        #    {
        #        dt = new DictTable(RecordUtil::commonTableId());
        #    }
        #
        #    return dt;
        #}
      ENDSOURCE
      SOURCE #findFromUniqueIndexFields
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// ищет по значениям из полей, которые входят в уникальный индекс
        #public static Common findFromUniqueIndexFields(Common rec)
        #{
        #    KeyData     keyData     = SysDictTable::getKeyData(rec).pack();
        #    Common      ret         = SysDictTable::findFromKeyData(rec.TableId, keyData);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #getFieldValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype getFieldValue(Common record, FieldId fieldid)
        #{
        #    // не делаем дополнительных проверок: к случае чего, аксапта выбросит исключение про поля в записи
        #    anytype value = record.(fieldid);
        #
        #    return value;
        #}
      ENDSOURCE
      SOURCE #getInfo
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает строку, которая содержит значения указанных в контейнере, ключевых и обязательных полей
        #// каждое поле задействовано в контейнере только один раз
        #//
        #// порядок возвращаемых значений:
        #// сначала идут значения из полей, которые указаны пользователем,
        #// затем полей из уникальных индексов, затем обязательные поля
        #//
        #// как правило, метод используется в информационных сообщениях
        #//
        #// пример использования:
        #// str s = SysDictTable::getInfo(rContractTable, [fieldnum(rContractTable,CurrencyCode)]);
        #//
        #// в стандартной Аксапте уникальный индекс состоит из полей RContractPartnerType, RContractCode, RContractAccount
        #// результат будет похож на: "RContractTable[CurrencyCode, RContractPartnerType, RContractCode, RContractAccount]"
        #public static str getInfo(
        #    Common      rec,
        #    anytype     fieldIdCollection       = null, // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #    anytype     uniqueCollection        = null, // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #    anytype     mandatoryCollection     = null) // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #{
        #    str valueInfo = RecordUtil::getValuesInfo(rec, fieldIdCollection, uniqueCollection, mandatoryCollection);
        #    str ret = strFmt('%1[%2]', tableId2name(rec.TableId), valueInfo);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #getValues
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает контейнер, который содержит значения указанных в контейнере, ключевых и обязательных полей
        #// каждое поле задействовано в контейнере только один раз
        #//
        #// порядок возвращаемых значений:
        #// сначала идут значения из полей, которые указаны пользователем,
        #// затем полей из уникальных индексов, затем обязательные поля
        #//
        #// как правило, метод используется в unit-тестах
        #//
        #// пример использования:
        #// container values = SysDictTable::getValues(rContractTable, [fieldnum(rContractTable,CurrencyCode)]);
        #//
        #// в стандартной Аксапте уникальный индекс состоит из полей RContractPartnerType, RContractCode, RContractAccount
        #// контейнер values будет содержать 4 значения: [CurrencyCode, RContractPartnerType, RContractCode, RContractAccount]
        #public static container getValues(
        #    Common      rec,
        #    anytype     fieldIdCollection       = null, // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #    anytype     uniqueCollection        = null, // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #    anytype     mandatoryCollection     = null) // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #{
        #    Set         fields  = new Set(Types::Integer);
        #    container   ret;
        #
        #    void appendFrom(anytype collection)
        #    {
        #        FieldId fieldId;
        #        Enumerator e = collection ? EnumeratorUtil::getEnumerator(collection) : null;
        #
        #        while( e && e.moveNext() )
        #        {
        #            fieldId = AnytypeUtil::asInt(e.current());
        #
        #            if( fieldId && fields.add(fieldId) )
        #            {
        #                ret += rec.(fieldId);
        #            }
        #        }
        #    }
        #
        #    appendFrom(fieldIdCollection);
        #    appendFrom(uniqueCollection ? uniqueCollection : SysDictTable::getUniqueIndexFields(rec.TableId));
        #    appendFrom(mandatoryCollection ? mandatoryCollection : SysDictTable::getMandatoryFields(rec.TableId));
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #getValuesInfo
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает контейнер, который содержит значения указанных в контейнере, ключевых и обязательных полей
        #// каждое поле задействовано в контейнере только один раз
        #//
        #// порядок возвращаемых значений:
        #// сначала идут значения из полей, которые указаны пользователем,
        #// затем полей из уникальных индексов, затем обязательные поля
        #//
        #// как правило, метод используется в unit-тестах
        #//
        #// пример использования:
        #// container values = SysDictTable::getValuesInfo(rContractTable, [fieldnum(rContractTable,CurrencyCode)]);
        #//
        #// в стандартной Аксапте уникальный индекс состоит из полей RContractPartnerType, RContractCode, RContractAccount
        #// контейнер values будет содержать 4 значения: [CurrencyCode, RContractPartnerType, RContractCode, RContractAccount]
        #public static str getValuesInfo(
        #    Common      rec,
        #    anytype     fieldIdCollection       = null, // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #    anytype     uniqueCollection        = null, // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #    anytype     mandatoryCollection     = null) // Integer, Container, List, Set, Map, implements Enumerable or getEnumerator method
        #{
        #    Set fields = new Set(Types::Integer);
        #    TextBuffer text;
        #
        #    void appendFrom(anytype collection)
        #    {
        #        FieldId fieldId;
        #        DictField df;
        #        str info;
        #        Enumerator e = EnumeratorUtil::getEnumerator(collection);
        #
        #        while( e && e.moveNext() )
        #        {
        #            fieldId = AnytypeUtil::asInt(e.current());
        #
        #            if( fieldId && fields.add(fieldId) )
        #            {
        #                df = new DictField(rec.TableId, fieldId);
        #                info = strfmt("%1=%2", df.label(), rec.(df.id()));
        #
        #                text = TextBufferUtil::appendIfNotEmpty(text, ', ');
        #                text = TextBufferUtil::append(text, info);
        #            }
        #        }
        #    }
        #
        #    appendFrom(fieldIdCollection);
        #    appendFrom(uniqueCollection ? uniqueCollection : SysDictTable::getUniqueIndexFields(rec.TableId));
        #    appendFrom(mandatoryCollection ? mandatoryCollection : SysDictTable::getMandatoryFields(rec.TableId));
        #
        #    return TextBufferUtil::getText(text);
        #}
      ENDSOURCE
      SOURCE #hasTableAccess
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean hasTableAccess(Common rec, AccessType neededAccessType)
        #{
        #    AccessType accessType = RecordUtil::dictTable(rec).rights();
        #    boolean ret = (accessType >= neededAccessType);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isCommon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isCommon(Common record)
        #{
        #    boolean ret = (record.TableId && record.TableId == RecordUtil::commonTableId());
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true, если таблица не содержит ни одной записи
        #// возвращает false, если хотя бы одна запись есть.
        #//
        #// метод особенно полезен для временных таблиц
        #public static boolean isEmpty(Common record)
        #{
        #    Common buf = RecordUtil::makeRecord(record.TableId);
        #
        #    if( record.isTmp() )
        #    {
        #        buf.setTmp();
        #        buf.setTmpData(record);
        #    }
        #
        #    select firstOnly buf;
        #
        #    return (buf.RecId == 0);
        #}
      ENDSOURCE
      SOURCE #isNull
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean isNull(Common record)
        #{
        #    boolean ret = (record.TableId && record.TableId == RecordUtil::commonTableId());
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #makeRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common makeRecord(TableId tableId = 0)
        #{
        #    Common record;
        #    DictTable dt;
        #
        #    if( tableId )
        #    {
        #        dt = new DictTable(tableId);
        #        if( dt )
        #        {
        #            record = dt.makeRecord();
        #        }
        #    }
        #
        #    return record;
        #}
      ENDSOURCE
      SOURCE #makeRecordIfCommon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common makeRecordIfCommon(Common record, TableId tableId)
        #{
        #    if( RecordUtil::isCommon(record) )
        #    {
        #        record = RecordUtil::makeRecord(tableId);
        #    }
        #
        #    return record;
        #}
      ENDSOURCE
      SOURCE #match
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// проверяет есть ли вхожение паттерна в какое-нибудь поле
        #// проверку выполняет рекурсивно, если есть поля-контейнеры
        #public static boolean match(
        #    str         pattern,
        #    Common      record,
        #    boolean     includeSystemFields     = false)
        #{
        #    SysDictTable    sysDictTable    = new SysDictTable(record.TableId);
        #    Set             fields          = sysDictTable.fields();
        #    SetEnumerator   fe              = fields ? fields.getEnumerator() : null;
        #    SysDictField    field;
        #    Any             value;
        #;
        #    while( fe && fe.moveNext() )
        #    {
        #        field = fe.current();
        #        if( !includeSystemFields && field.isSystem() )
        #            continue;
        #
        #        switch( field.type() )
        #        {
        #            case Types::String:
        #            case Types::RString:
        #            case Types::VarString:
        #                value = Any::field(record, field.id());
        #                if( match(pattern, value.asString()) )
        #                    return true;
        #                break;
        #
        #            case Types::Container:
        #                value = Any::field(record, field.id());
        #                if( ConUtil::match(pattern, value.asContainer()) )
        #                    return true;
        #                break;
        #
        #            case Types::Record:
        #                value = Any::field(record, field.id());
        #                if( RecordUtil::match(pattern, value.asRecord()) )
        #                    return true;
        #                break;
        #
        #            default:
        #                break;
        #        }
        #    }
        #
        #    return false;
        #}
      ENDSOURCE
      SOURCE #setFieldValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static anytype setFieldValue(Common record, fieldId fieldid, anytype value)
        #{
        #    // не делаем дополнительных проверок: к случае чего, аксапта выбросит исключение про поля в записи
        #    record.(fieldid) = value;
        #
        #    return value;
        #}
      ENDSOURCE
      SOURCE #sysDictTable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// всегда возвращает not nullable объект. Полезно в выражениях
        #public static SysDictTable sysDictTable(Common rec)
        #{
        #    SysDictTable dt = new SysDictTable(rec.TableId);
        #
        #    if( !dt )
        #    {
        #        new SysDictTable(RecordUtil::commonTableId());
        #    }
        #
        #    return dt;
        #}
      ENDSOURCE
      SOURCE #toCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container toCon(Common rec)
        #{
        #    // первый элемент - для совместимости с Global:buf2con(), и Global::con2Buf()
        #    container ret = [rec.TableId, rec];
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #validateAndDelete
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean validateAndDelete(Common buf)
        #{
        #    if( !buf.validateDelete() )
        #    {
        #        return false;
        #    }
        #
        #    buf.delete();
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #validateAndDeleteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common validateAndDeleteOrThrow(Common buf)
        #{;
        #    RecordUtil::validateDeleteOrThrow(buf);
        #    buf.delete();
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #validateAndInsert
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean validateAndInsert(Common buf)
        #{
        #    if( !buf.validateWrite() )
        #    {
        #        return false;
        #    }
        #
        #    buf.insert();
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #validateAndInsertOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common validateAndInsertOrThrow(Common buf)
        #{;
        #    RecordUtil::validateWriteOrThrow(buf);
        #    buf.insert();
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #validateAndUpdate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean validateAndUpdate(Common buf)
        #{
        #    if( !buf.validateWrite() )
        #    {
        #        return false;
        #    }
        #
        #    buf.update();
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #validateAndUpdateOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common validateAndUpdateOrThrow(Common buf)
        #{;
        #    RecordUtil::validateWriteOrThrow(buf);
        #    buf.update();
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #validateAndWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean validateAndWrite(Common buf)
        #{
        #    if( !buf.validateWrite() )
        #    {
        #        return false;
        #    }
        #
        #    buf.write();
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #validateAndWriteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common validateAndWriteOrThrow(Common buf)
        #{;
        #    RecordUtil::validateWriteOrThrow(buf);
        #    buf.write();
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #validateDelete
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean validateDelete(Common buf)
        #{
        #    if( !buf.validateDelete() )
        #    {
        #        return false;
        #    }
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #validateDeleteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common validateDeleteOrThrow(Common buf)
        #{
        #    if( !buf.validateDelete() )
        #    {
        #        throw error(strFmt('Delete validation error for %1', RecordUtil::getInfo(buf)));
        #    }
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #validateWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean validateWrite(Common buf)
        #{
        #    if( !buf.validateWrite() )
        #    {
        #        return false;
        #    }
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #validateWriteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common validateWriteOrThrow(Common buf)
        #{
        #    if( !buf.validateWrite() )
        #    {
        #        throw error(strFmt('Write validation error for %1', RecordUtil::getInfo(buf)));
        #    }
        #
        #    return buf;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysRecordList unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysRecordList
    PROPERTIES
      Name                #SysRecordList
      Extends             #List
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #addEnd
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public anytype addEnd(anytype value)
        #{
        #    Common buf = this.assignable(value);
        #
        #    if( this.isAssignableBuf(buf) )
        #    {
        #        buf = super(value);
        #        this.tableIds().add(buf.TableId);
        #    }
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #addStart
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public anytype addStart(anytype value)
        #{
        #    Common buf = this.assignable(value);
        #
        #    if( this.isAssignableBuf(buf) )
        #    {
        #        buf = super(value);
        #        this.tableIds().add(buf.TableId);
        #    }
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #appendEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает число добавленных элементов
        #public int appendEnumerator(Enumerator e, int maxElements = 0)
        #{
        #    int elements;
        #    Common buf;
        #
        #    while( e && e.moveNext() && (maxElements == 0 || elements <= maxElements)  )
        #    {
        #        buf = this.addEnd(e.current());
        #        elements += this.isAssignableBuf(buf);
        #    }
        #
        #    return elements;
        #}
      ENDSOURCE
      SOURCE #appendList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// @override
        #public void appendList(List list)
        #{
        #    if( list &&
        #        confind([Types::Record, Types::Integer, Types::String, Types::RString, Types::VarString], list.typeId()) )
        #    {
        #        this.appendEnumerator(list.getEnumerator());
        #    }
        #}
      ENDSOURCE
      SOURCE #appendMarked
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет все отмеченные на форме записи
        #// возвращает число добавленных элементов
        #public int appendMarked(FormDataSource fds, int maxElements = 0)
        #{
        #    int elements;
        #    common buf = fds ? fds.getFirst(1) : null;
        #
        #    while( fds && buf && (maxElements == 0 || elements <= maxElements) )
        #    {
        #        buf = this.addEnd(buf);
        #        elements = this.isAssignableBuf(buf);
        #
        #        buf = fds.getNext();
        #    }
        #
        #    return elements;
        #}
      ENDSOURCE
      SOURCE #appendQuery
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет в список datasource из query, которые получат значения в выборке из базы
        #// Получится список неинициализированых буферов
        #// возвращает число добавленных элементов
        #public int appendQuery(Query q, int maxElements = 0)
        #{
        #    int elements;
        #    QueryBuildDataSource qbds;
        #    int qbdsId;
        #
        #    if( q )
        #    {
        #        qbdsId = q.nextUniqueId();
        #        while( qbdsId && (maxElements == 0 || elements <= maxElements) )
        #        {
        #            qbds = q.dataSourceUniqueId(qbdsId);
        #
        #            if (qbds &&
        #                qbds.table() &&
        #                qbds.enabled() &&
        #                confind([JoinMode::InnerJoin, JoinMode::OuterJoin], qbds.joinMode()))
        #            {
        #                elements += this.addEnd(qbds.table());
        #            }
        #
        #            qbdsId = q.nextUniqueId();
        #        }
        #    }
        #
        #    return elements;
        #}
      ENDSOURCE
      SOURCE #appendRecIdEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет табличные буфера с указанными RecId
        #// возвращает число добавленных элементов
        #public int appendRecIdEnumerator(TableId tableId, Enumerator e, boolean selectForUpdate = false, int maxElements = 0)
        #{
        #    int       elements;
        #    RecId     recId;
        #    common    buf;
        #
        #    while( e && e.moveNext() && (maxElements == 0 || elements <= maxElements)  )
        #    {
        #        recId = AnytypeUtil::asRecId(e.current());
        #        if( recId )
        #        {
        #            buf = RecordUtil::commonRecord(); // на случай, если в списке табличные буфера разного типа
        #            buf.selectForUpdate(selectForUpdate);
        #            select firstonly buf where buf.RecId == recId;
        #
        #            buf = this.addEnd(e.current());
        #            elements += this.isAssignableBuf(buf);
        #        }
        #    }
        #
        #    return elements;
        #}
      ENDSOURCE
      SOURCE #appendSelect
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет все записи из табличного буфера, который был заполнен командой select
        #// возвращает число добавленных элементов
        #//
        #// пример:
        #//
        #// {
        #//     CustTable ct;
        #//     select ct where ct.Name like 'Micro*';
        #//     SysRecordList::fromList(ct);
        #// }
        #//
        #public int appendSelect(Common selectedBuf, int maxElements = 0)
        #{
        #    int elements;
        #    Common buf;
        #
        #    while( selectedBuf && (maxElements == 0 || elements <= maxElements)  )
        #    {
        #        buf = this.addEnd(selectedBuf);
        #        elements += this.isAssignableBuf(buf);
        #
        #        next selectedBuf;
        #    }
        #
        #    return elements;
        #}
      ENDSOURCE
      SOURCE #assignable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Common assignable(anytype value)
        #{
        #    Common buf = AnytypeUtil::toBuf(value);
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// класс содержит список записей из таблиц *разного типа* (в том числе могут быть типа Common)
        #// для записей всех типов вполне успешно срабатывают табличные методы initValue, validateWrite, write, insert, update и т.д.
        #// класс позволяет единообразно обслуживать записи из списка
        #//
        #// класс во многом похож на RecordLinkList. отличия:
        #// * работает предсказуемым образом
        #// * гарантировано держит все записи в памяти
        #// * позволяет создавать несколько энумераторов и делать вложенные циклы по одному списку
        #// * не вмешивается и не копирует записи, поэтому внутренние флаги и проперти в записи сохраняются.
        #//   это значит что:
        #//   * методы update получат правильный флаг forupdate
        #//   * оператор next будет правильно работать с записями, которые выбраны из списка
        #//   * запись из map останется записью из map
        #// * методы pack/unpack позволяют эффективно передавать значения между клиентом и сервером (внутренние флаги и проперти сбрасываются)
        #// * методы validateDelete и validateWrite сначала проверят все записи, лишь потом выполнят действия для всех записей
        #// * реализованы методы merge и appendList и другие методы базового класса List
        #//
        #// по сравнению с RecordLinkList этот класс не позволяет:
        #// * получить доступ к записи по индексу (см. второй параметр в RecordLinkList.get)
        #// * перейти к предыдущей записи в цикле (см. метод RecordLinkList.prev)
        #// * удалять элементы из списка
        #//
        #// TODO добавить выборку табличных буферов из QueryRun
        #class SysRecordList extends List
        #{
        #    Set tableIds;
        #}
      ENDSOURCE
      SOURCE #deleteDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет удаление без валиадции.
        #// Может, стоит использовать метод validateAndDelete?
        #public SysRecordList deleteDatabase()
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( le && le.moveNext() )
        #    {
        #        buf = le.current();         // строка для отладчика
        #        buf.delete();
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #hasTableAccess
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean hasTableAccess(AccessType neededAccessType)
        #{
        #    AccessType accessType = this.rights();
        #    boolean ret = (accessType >= neededAccessType);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #initValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет initValue для буферов с пустым recId.
        #public void initValue()
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( le && le.moveNext() )
        #    {
        #        buf = le.current();
        #        if( !buf.RecId )
        #        {
        #            buf.initValue();
        #        }
        #    }
        #}
      ENDSOURCE
      SOURCE #insertDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет вставку без валиадции для буферов с пустым recId.
        #// Может, стоит использовать метод validateAndInsert?
        #public SysRecordList insertDatabase()
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( le && le.moveNext() )
        #    {
        #        buf = le.current();
        #        if( !buf.RecId )
        #        {
        #            buf.insert();
        #        }
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #isAssignableBuf
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean isAssignableBuf(Common buf)
        #{
        #    boolean ret = buf.TableId && !RecordUtil::isCommon(buf);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void new(Types _type = Types::Record)
        #{
        #    anytype contract = Debug::assert(_type == Types::Record);
        #
        #    super(Types::Record);
        #}
      ENDSOURCE
      SOURCE #rights
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает минимальный уровень доступа
        #// пустой список записей имеет максимальный уровень доступа
        #public AccessType rights()
        #{
        #    AccessType ret = AccessType::Delete;
        #    AccessType rights;
        #
        #    SetEnumerator se = this.tableIds().getEnumerator();
        #    TableId tableId;
        #
        #    while( se && se.moveNext() )
        #    {
        #        tableId = se.current();
        #        rights = new DictTable(tableId).rights(); // так мы получаем права с учетом свойства таблицы maxAccessMode
        #        ret = min(ret, rights);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #selectForUpdate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// устанавливает режим выборки для всех табличных буферов в true, если selectForUpdate == true
        #// не изменяет флаг у табличных буферов, если selectForUpdate == false
        #public void selectForUpdate(boolean selectForUpdate = false)
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( selectForUpdate && le && le.moveNext() )
        #    {
        #        buf = le.current();
        #        buf.selectForUpdate(selectForUpdate);
        #    }
        #}
      ENDSOURCE
      SOURCE #setTmp
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// сделать все записи временными
        #public void setTmp()
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( le && le.moveNext() )
        #    {
        #        buf = le.current();         // строка для отладчика
        #        buf.setTmp();
        #    }
        #}
      ENDSOURCE
      SOURCE #tableIds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Set tableIds()
        #{
        #    if( !tableIds )
        #        tableIds = new Set(Types::Integer);
        #
        #    return tableIds;
        #}
      ENDSOURCE
      SOURCE #updateDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет обновление без валиадции для буферов с заполненным recId.
        #// Может, стоит использовать метод validateAndupdate?
        #public SysRecordList updateDatabase()
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( le && le.moveNext() )
        #    {
        #        buf = le.current();
        #        if( buf.RecId )
        #        {
        #            buf.update();
        #        }
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateAndDelete
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех записей,
        #// а затем, если валидация успешна, удаляет
        #public boolean validateAndDelete()
        #{
        #    boolean valid = this.validateDelete();
        #
        #    if( valid )
        #    {
        #        this.deleteDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndDeleteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysRecordList validateAndDeleteOrThrow()
        #{
        #    this.validateDeleteOrThrow();
        #    this.deleteDatabase();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateAndInsert
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех еще не вставленных записей (recId == 0),
        #// а затем, если валидация успешна, вставку еще не вставленных записей
        #public boolean validateAndInsert()
        #{
        #    boolean valid = this.validateWrite();
        #
        #    if( valid )
        #    {
        #        this.insertDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndInsertOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysRecordList validateAndInsertOrThrow()
        #{
        #    this.validateWriteOrThrow();
        #    this.insertDatabase();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateAndUpdate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех уже вставленных записей (recId != 0),
        #// а затем, если валидация успешна, update уже вставленных записей
        #public boolean validateAndUpdate()
        #{
        #    boolean valid = this.validateWrite();
        #
        #    if( valid )
        #    {
        #        this.updateDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndUpdateOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysRecordList validateAndUpdateOrThrow()
        #{
        #    this.validateWriteOrThrow();
        #    this.writeDatabase();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateAndWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех записей,
        #// а затем, если валидация успешна, запись всех буферов (Аксапта сама разбирается нужно вставить или обновить буфер)
        #public boolean validateAndWrite()
        #{
        #    boolean valid = this.validateWrite();
        #
        #    if( valid )
        #    {
        #        this.writeDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndWriteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysRecordList validateAndWriteOrThrow()
        #{
        #    this.validateWriteOrThrow();
        #    this.writeDatabase();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateDelete
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean validateDelete()
        #{
        #    boolean valid = true;
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( valid && le && le.moveNext() )
        #    {
        #        buf = le.current();         // строка для отладчика
        #        valid = buf.validateDelete();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateDeleteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysRecordList validateDeleteOrThrow()
        #{
        #    if( !this.validateDelete() )
        #    {
        #        throw error(strFmt('Delete validation error in %1', funcname()));
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean validateWrite()
        #{
        #    boolean valid = true;
        #    ListEnumerator le = this.getEnumerator();
        #    Common buf;
        #
        #    while( valid && le && le.moveNext() )
        #    {
        #        buf = le.current();         // строка для отладчика
        #        valid = buf.validateWrite();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateWriteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysRecordList validateWriteOrThrow()
        #{
        #    if( !this.validateWrite() )
        #    {
        #        throw error(strFmt('Write validation error in %1', funcname()));
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #writeDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет запись без валиадции всех буферов.
        #// Может, стоит использовать метод validateAndWrite?
        #public SysRecordList writeDatabase()
        #{
        #    ListEnumerator le = this.getEnumerator();
        #    Common rec;
        #
        #    while( le && le.moveNext() )
        #    {
        #        rec = le.current(); // строка для отладчика
        #        rec.write();
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList construct()
        #{
        #    SysRecordList recordList = new SysRecordList(Types::Record);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// табличные буфера как параметры, чтобы не потерять внутренние флаги
        #// такие как SelectForUpdate, formDataSource, был сделан select, isTmp
        #public static SysRecordList fromBuf(
        #    Common buf1 = null,
        #    Common buf2 = null,
        #    Common buf3 = null,
        #    Common buf4 = null,
        #    Common buf5 = null,
        #    Common buf6 = null,
        #    Common buf7 = null,
        #    Common buf8 = null,
        #    Common buf9 = null,
        #    Common buf10 = null,
        #    Common buf11 = null,
        #    Common buf12 = null)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    if( !prmisdefault(buf1) ) recordList.addEnd(buf1);
        #    if( !prmisdefault(buf2) ) recordList.addEnd(buf2);
        #    if( !prmisdefault(buf3) ) recordList.addEnd(buf3);
        #    if( !prmisdefault(buf4) ) recordList.addEnd(buf4);
        #    if( !prmisdefault(buf5) ) recordList.addEnd(buf5);
        #    if( !prmisdefault(buf6) ) recordList.addEnd(buf6);
        #    if( !prmisdefault(buf7) ) recordList.addEnd(buf7);
        #    if( !prmisdefault(buf8) ) recordList.addEnd(buf8);
        #    if( !prmisdefault(buf9) ) recordList.addEnd(buf9);
        #    if( !prmisdefault(buf10) ) recordList.addEnd(buf10);
        #    if( !prmisdefault(buf11) ) recordList.addEnd(buf11);
        #    if( !prmisdefault(buf12) ) recordList.addEnd(buf12);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf1
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromBuf1(Common buf)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    recordList.addEnd(buf);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf2
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromBuf2(Common buf1, Common buf2)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    recordList.addEnd(buf1);
        #    recordList.addEnd(buf2);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf3
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromBuf3(Common buf1, Common buf2, Common buf3)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    recordList.addEnd(buf1);
        #    recordList.addEnd(buf2);
        #    recordList.addEnd(buf3);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf4
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromBuf4(Common buf1, Common buf2, Common buf3, Common buf4)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    recordList.addEnd(buf1);
        #    recordList.addEnd(buf2);
        #    recordList.addEnd(buf3);
        #    recordList.addEnd(buf4);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf5
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromBuf5(Common buf1, Common buf2, Common buf3, Common buf4, Common buf5)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    recordList.addEnd(buf1);
        #    recordList.addEnd(buf2);
        #    recordList.addEnd(buf3);
        #    recordList.addEnd(buf4);
        #    recordList.addEnd(buf5);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBuf6
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromBuf6(Common buf1, Common buf2, Common buf3, Common buf4, Common buf5, Common buf6)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #
        #    recordList.addEnd(buf1);
        #    recordList.addEnd(buf2);
        #    recordList.addEnd(buf3);
        #    recordList.addEnd(buf4);
        #    recordList.addEnd(buf5);
        #    recordList.addEnd(buf6);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromBufCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// табличные буфера в контейнере потеряют внутренние флаги
        #// такие как SelectForUpdate, formDataSource, был сделан select, isTmp
        #public static SysRecordList fromBufCon(container records, TableId tableId = 0)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    Common buf;
        #    int len = conlen(records);
        #    int i;
        #
        #    for (i=1; i<=len; i++)
        #    {
        #        buf = Any::conpeek(records, i).asRecord(tableId);
        #        recordList.addEnd(buf);
        #    }
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromCon(container tableIds_tableNames_records)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    int len = conlen(tableIds_tableNames_records);
        #    int i;
        #
        #    for (i=1; i<=len; i++)
        #    {
        #        recordList.addEnd(conpeek(tableIds_tableNames_records, i));
        #    }
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromEnumerator(Enumerator e, int maxElements = 0)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    ;
        #
        #    recordList.appendEnumerator(e, maxElements);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает объект, содержащий клон списка
        #// внимание: объекты (элементы списка) являются общими для старого и нового списка, а сами списки - разные
        #public static SysRecordList fromList(List list)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    ;
        #
        #    recordList.appendList(list);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromMarked
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет все отмеченные на форме записи
        #// возвращает число добавленных элементов
        #public static SysRecordList fromMarked(FormDataSource fds, int maxElements = 0)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    ;
        #
        #    recordList.appendMarked(fds, maxElements);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromQuery
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет в список те datasource из query, которые получат значения в выборке из базы
        #public static SysRecordList fromQuery(Query q, int maxElements = 0)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    ;
        #
        #    recordList.appendQuery(q, maxElements);
        #
        #    return recordList;
        #}
      ENDSOURCE
      SOURCE #fromSelect
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет все записи из буфера, который был заполнен командой select
        #// возвращает число добавленных элементов
        #//
        #// пример:
        #//
        #// {
        #//     ...
        #//     select custVendTable where custVendTable.Name like 'Micro*';
        #//     recordList = SysRecordList::fromSelect(custVendTable);
        #//     rle = recordList.getEnumerator()
        #//
        #//     while( rle && rle.moveNext() )
        #//     {
        #//        rle.current(); // возвращает табличный буфер
        #//     }
        #// }
        #//
        #public static SysRecordList fromSelect(Common rec, int maxElements = 0)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    ;
        #
        #    recordList.appendSelect(rec, maxElements);
        #
        #    return recordList;
        #}
        #
        #
        #
      ENDSOURCE
      SOURCE #fromTableId
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordList fromTableId(TableId tableId)
        #{
        #    SysRecordList recordList = SysRecordList::construct();
        #    ;
        #
        #    recordList.addEnd(tableId);
        #
        #    return recordList;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysRecordMap unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysRecordMap
    PROPERTIES
      Name                #SysRecordMap
      Extends             #Map
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #assignable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Common assignable(anytype value)
        #{
        #    Common buf = AnytypeUtil::toBuf(value);
        #
        #    return buf;
        #}
      ENDSOURCE
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// класс содержит map записей из таблиц *разного типа* (в том числе могут быть типа Common)
        #// для записей всех типов вполне успешно срабатывают табличные методы initValue, validateWrite, write, insert, update и т.д.
        #// класс позволяет единообразно обслуживать записи из списка
        #//
        #class SysRecordMap extends Map
        #{
        #    Set tableIds;
        #}
      ENDSOURCE
      SOURCE #deleteDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет удаление без валиадции.
        #// Может, стоит использовать метод validateAndDelete?
        #public SysRecordMap deleteDatabase()
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( me && me.moveNext() )
        #    {
        #        buf = me.currentValue();         // строка для отладчика
        #        buf.delete();
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #hasTableAccess
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean hasTableAccess(AccessType neededAccessType)
        #{
        #    AccessType accessType = this.rights();
        #    boolean ret = (accessType >= neededAccessType);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #initValue
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет initValue для буферов с пустым recId.
        #public void initValue()
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( me && me.moveNext() )
        #    {
        #        buf = me.current();
        #        if( !buf.RecId )
        #        {
        #            buf.initValue();
        #        }
        #    }
        #}
      ENDSOURCE
      SOURCE #insert
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean insert(anytype key, anytype value)
        #{
        #    boolean ret;
        #    Common buf = this.assignable(value);
        #
        #    if( this.isAssignableBuf(buf) )
        #    {
        #        ret = super(key, value);
        #        this.tableIds().add(buf.TableId);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #insertDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет вставку без валиадции для буферов с пустым recId.
        #// Может, стоит использовать метод validateAndDelete?
        #public SysRecordMap insertDatabase()
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( me && me.moveNext() )
        #    {
        #        buf = me.current();
        #        if( !buf.RecId )
        #        {
        #            buf.insert();
        #        }
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #isAssignableBuf
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean isAssignableBuf(Common buf)
        #{
        #    boolean ret = buf.TableId && !RecordUtil::isCommon(buf);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void new(Types _keyType, Types _valueType = Types::Record)
        #{
        #    anytype contract = Debug::assert(_valueType == Types::Record);
        #
        #    super(_keyType, Types::Record);
        #}
      ENDSOURCE
      SOURCE #rights
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает минимальный уровень доступа
        #// пустой список записей имеет максимальный уровень доступа
        #public AccessType rights()
        #{
        #    AccessType ret = AccessType::Delete;
        #    AccessType rights;
        #
        #    SetEnumerator se = this.tableIds().getEnumerator();
        #    TableId tableId;
        #
        #    while( se && se.moveNext() )
        #    {
        #        tableId = se.current();
        #        rights = new DictTable(tableId).rights(); // так мы получаем права с учетом свойства таблицы maxAccessMode
        #        ret = min(ret, rights);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #selectForUpdate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// устанавливает режим выборки для всех табличных буферов в true, если selectForUpdate == true
        #// не изменяет флаг у табличных буферов, если selectForUpdate == false
        #public void selectForUpdate(boolean selectForUpdate = false)
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( selectForUpdate && me && me.moveNext() )
        #    {
        #        buf = me.currentValue();
        #        buf.selectForUpdate(selectForUpdate);
        #    }
        #}
      ENDSOURCE
      SOURCE #setTmp
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void setTmp()
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( me && me.moveNext() )
        #    {
        #        buf = me.currentValue();         // строка для отладчика
        #        buf.setTmp();
        #    }
        #}
      ENDSOURCE
      SOURCE #tableIds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Set tableIds()
        #{
        #    if( !tableIds )
        #        tableIds = new Set(Types::Integer);
        #
        #    return tableIds;
        #}
      ENDSOURCE
      SOURCE #updateDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет обновление без валиадции для буферов с заполненным recId.
        #// Может, стоит использовать метод validateAndDelete?
        #public SysRecordMap updateDatabase()
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( me && me.moveNext() )
        #    {
        #        buf = me.currentValue();
        #        if( buf.RecId )
        #        {
        #            buf.update();
        #        }
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #validateAndDelete
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех записей,
        #// а затем, если валидация успешна, удаляет
        #public boolean validateAndDelete()
        #{
        #    boolean valid = this.validateDelete();
        #
        #    if( valid )
        #    {
        #        this.deleteDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndDeleteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void validateAndDeleteOrThrow()
        #{
        #    this.validateDeleteOrThrow();
        #    this.deleteDatabase();
        #}
      ENDSOURCE
      SOURCE #validateAndInsert
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех еще не вставленных записей (recId == 0),
        #// а затем, если валидация успешна, вставку еще не вставленных записей
        #public boolean validateAndInsert()
        #{
        #    boolean valid = this.validateWrite();
        #
        #    if( valid )
        #    {
        #        this.insertDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndInsertOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void validateAndInsertOrThrow()
        #{
        #    this.validateWriteOrThrow();
        #    this.insertDatabase();
        #}
      ENDSOURCE
      SOURCE #validateAndUpdate
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех уже вставленных записей (recId != 0),
        #// а затем, если валидация успешна, update уже вставленных записей
        #public boolean validateAndUpdate()
        #{
        #    boolean valid = this.validateWrite();
        #
        #    if( valid )
        #    {
        #        this.updateDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndUpdateOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void validateAndUpdateOrThrow()
        #{
        #    this.validateWriteOrThrow();
        #    this.writeDatabase();
        #}
      ENDSOURCE
      SOURCE #validateAndWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет валидацию всех записей,
        #// а затем, если валидация успешна, запись всех буферов (Аксапта сама разбирается нужно вставить или обновить буфер)
        #public boolean validateAndWrite()
        #{
        #    boolean valid = this.validateWrite();
        #
        #    if( valid )
        #    {
        #        this.writeDatabase();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateAndWriteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void validateAndWriteOrThrow()
        #{
        #    this.validateWriteOrThrow();
        #    this.writeDatabase();
        #}
      ENDSOURCE
      SOURCE #validateDelete
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean validateDelete()
        #{
        #    boolean valid = true;
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( valid && me && me.moveNext() )
        #    {
        #        buf = me.currentValue();         // строка для отладчика
        #        valid = buf.validateDelete();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateDeleteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void validateDeleteOrThrow()
        #{
        #    if( !this.validateDelete() )
        #    {
        #        throw error(strFmt('Delete validation error in %1', funcname()));
        #    }
        #}
      ENDSOURCE
      SOURCE #validateWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean validateWrite()
        #{
        #    boolean valid = true;
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( valid && me && me.moveNext() )
        #    {
        #        buf = me.currentValue();         // строка для отладчика
        #        valid = buf.validateWrite();
        #    }
        #
        #    return valid;
        #}
      ENDSOURCE
      SOURCE #validateWriteOrThrow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public void validateWriteOrThrow()
        #{
        #    if( !this.validateWrite() )
        #    {
        #        throw error(strFmt('Write validation error in %1', funcname()));
        #    }
        #}
      ENDSOURCE
      SOURCE #writeDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// выполняет запись без валиадции всех буферов.
        #// Может, стоит использовать метод validateAndDelete?
        #public SysRecordMap writeDatabase()
        #{
        #    MapEnumerator me = this.getEnumerator();
        #    Common buf;
        #
        #    while( me && me.moveNext() )
        #    {
        #        buf = me.currentValue(); // строка для отладчика
        #        buf.writeDatabase();
        #    }
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysRecordMap construct(Types keyType)
        #{
        #    SysRecordMap recordMap = new SysRecordMap(keyType, Types::Record);
        #
        #    return recordMap;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysRecordInsertList unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysRecordInsertList
    PROPERTIES
      Name                #SysRecordInsertList
      Extends             #RecordInsertList
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #add
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет запись к набору RecordInsertList
        #// перед добавлением выполняет validateVrite, если это не запрещено параметрами класса
        #public int add(Common record)
        #{
        #    int ret;
        #
        #    if (this.skipValidateWrite() || record.validateWrite())
        #    {
        #        ret = super(record);    // внимание! на клиенте всегда возвращает 0
        #        elements++;
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class SysRecordInsertList extends RecordInsertlist
        #{
        #    boolean skipValidateWrite;
        #
        #    int elements;
        #    int lastInserted;
        #}
      ENDSOURCE
      SOURCE #elements
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// всего элементов было добавлено в этот список
        #public int elements()
        #{
        #    return elements;
        #}
      ENDSOURCE
      SOURCE #insertDatabase
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает число записей, которые были вставлены в базу данной командой
        #public int insertDatabase()
        #{
        #    int ret;
        #
        #    if( elements )
        #    {
        #        ret = super();
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #skipValidateWrite
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean skipValidateWrite(boolean _skipValidateWrite = skipValidateWrite)
        #{
        #    skipValidateWrite = _skipValidateWrite;
        #    return skipValidateWrite;
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// создает объект на сервере
        #// используйте new(), если вы хотите вручную управлять местом создания объекта
        #public server static SysRecordInsertList construct(
        #    TableId     tableId,
        #    boolean     skipInsertMethod    = false,
        #    boolean     skipDatabaseLog     = false,
        #    boolean     skipEvents          = false,
        #    boolean     skipAosValidation   = false,
        #    boolean     skipRLSValidation   = false,
        #    boolean     skipValidateWrite   = false)
        #{
        #    SysRecordInsertList ril = new SysRecordInsertList(
        #                                    tableId,
        #                                    skipInsertMethod,
        #                                    skipDatabaseLog,
        #                                    skipEvents,
        #                                    skipAosValidation,
        #                                    skipRLSValidation);
        #    ;
        #
        #    ril.skipValidateWrite(skipValidateWrite);
        #
        #    return ril;
        #}
      ENDSOURCE
      SOURCE #fromRecord
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// создает объект на сервере
        #// используйте new(), если вы хотите вручную управлять местом создания объекта
        #public server static SysRecordInsertList fromRecord(
        #    Common      record,
        #    boolean     skipValidateWrite   = false,
        #    boolean     skipRLSValidation   = record.recordLevelSecurity())
        #{
        #    SysRecordInsertList     ril     = SysRecordInsertList::construct(
        #                                            record.tableId,
        #                                            record.skipDataMethods(),
        #                                            record.skipDatabaseLog(),
        #                                            record.skipEvents(),
        #                                            record.skipAosValidation(),
        #                                            skipRLSValidation);
        #    ;
        #
        #    ril.skipValidateWrite(skipValidateWrite);
        #
        #    return ril;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: StrUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #StrUtil
    PROPERTIES
      Name                #StrUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class StrUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #after
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится после первого вхожения подстроки
        #// возвращает исходную строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      StringUtil::after('ABC-DEF-GH','DEF-') == 'GH'
        #//      StringUtil::after('ABC-DEF-GH',',')  == 'ABC-DEF-GH'
        #//
        #static str after(str s, str txt)
        #{
        #    int len = strlen(s);
        #    int pos = strscan(s, txt, 1, len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = substr(s, pos+strlen(txt), len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #afterChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится после первого вхожения любого из указанных символов characters
        #// возвращает исходную строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-GH','-=') == 'DEF-GH'
        #//      strBefore('ABC-DEF-GH',',')  == 'ABC-DEF-GH'
        #//
        #static str afterChar(str s, str characters)
        #{
        #    int len = strlen(s);
        #    int pos = strfind(s, characters, 1, len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = substr(s, pos+1, len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #afterLast
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится после последнего вхожения подстроки
        #// возвращает исходную строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-DEFGH','DEF') == 'GH'
        #//      strBefore('ABC-DEF-GH',',')  == 'ABC-DEF-GH'
        #//
        #static str afterLast(str s, str txt)
        #{
        #    int len = strlen(s);
        #    int pos = strscan(s, txt, len, -len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = substr(s, pos+strlen(txt), len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #afterLastChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится после последнего вхожения одного из символов characters
        #// возвращает исходную строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-GH','-=') == 'GH'
        #//      strBefore('ABC-DEF-GH',',')  == 'ABC-DEF-GH'
        #//
        #static str afterLastChar(str s, str characters)
        #{
        #    int len = strlen(s);
        #    int pos = strfind(s, characters, len, -len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = substr(s, pos+1, len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #before
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится до первого вхожения подстроки
        #// возвращает пустую строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-DEFGH','DEF') == 'ABC-'
        #//      strBefore('ABC-DEF-GH',',')  == ''
        #//
        #static str before(str s, str txt)
        #{
        #    int len = strlen(s);
        #    int pos = strscan(s, txt, 1, len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = StrUtil::strLeft(s, pos-1);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #beforeChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится до первого вхожения одного из символов characters
        #// возвращает пустую строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-GH','-=') == 'ABC'
        #//      strBefore('ABC-DEF-GH',',')  == ''
        #//
        #static str beforeChar(str s, str characters)
        #{
        #    int len = strlen(s);
        #    int pos = strfind(s, characters, 1, len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = StrUtil::strLeft(s, pos-1);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #beforeLast
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится до последнего вхожения подстроки
        #// возвращает пустую строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-DEFGH','-=') == 'ABC-DEF-'
        #//      strBefore('ABC-DEF-GH',',')  == ''
        #//
        #static str beforeLast(str s, str txt)
        #{
        #    int len = strlen(s);
        #    int pos = strscan(s, txt, len, -len);
        #    str ret = s;
        #
        #    if(pos)
        #        ret = StrUtil::strLeft(s, pos-1);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #beforeLastChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку, которая находится до последнего вхожения одного из символов characters
        #// возвращает пустую строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      strBefore('ABC-DEF-GH','-=') == 'ABC'
        #//      strBefore('ABC-DEF-GH',',')  == ''
        #//
        #static str beforeLastChar(str s, str characters)
        #{
        #    int len = strlen(s);
        #    int pos = strfind(s, characters, len, -len);
        #    str ret = s;
        #
        #    if(pos)
        #    {
        #        ret = StrUtil::strLeft(s, pos-1);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #drop
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// удалить первые n символов, если n > 0
        #// удалить последние n символов, если n < 0
        #// оставить исходную строку, если n == 0
        #public static str drop(str s, int n)
        #{
        #    if(n == 0)
        #        return s;
        #
        #    if(n < 0)
        #        return substr(s, 1, strlen(s)+n);
        #
        #    return substr(s, n+1, strlen(s)-n);
        #}
      ENDSOURCE
      SOURCE #end
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку заданной длины справа
        #//
        #// по идее, различие между strRight и end должно проявляться на текстах с разным направлением письма
        #static str end(str s, int len)
        #{
        #    str ret;
        #
        #    if( len <= 0 )
        #        return '';
        #
        #    ret = substr(s, strlen(s), -len);
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #hexDump
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str hexDump(str s)
        #{
        #    int         i;
        #    str         hex;
        #    int         len     = strlen(s);
        #    TextBuffer  ret     = new TextBuffer();
        #;
        #    for( i=1; i<=len; ++i )
        #    {
        #        if( ret.size() )
        #            ret.appendText(' ');
        #
        #        hex = Int2Hex(char2num(s, i));
        #
        #        if( strlen(hex) mod 2 )
        #        {
        #            ret.appendText('0');
        #        }
        #
        #        ret.appendText(hex);
        #    }
        #
        #    return ret.getText();
        #}
      ENDSOURCE
      SOURCE #isBlank
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #//TODO
        #public static boolean isBlank(str s)
        #{
        #    str ret;
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #isEmptyOrBlank
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #//TODO
        #public static boolean isEmptyOrBlank(str s)
        #{
        #    str ret;
        #
        #    return true;
        #}
      ENDSOURCE
      SOURCE #padEnd
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет указанный символ справа до нужной длины
        #// если строка уже длиннее, то ничего не делает. Поэтому результат может быть длиннее указанной длины.
        #//
        #// по идее, различие между padRight и padEnd должно проявляться на текстах с разным направлением письма
        #// но в аксапте никаких различий нет
        #static str padEnd(str s, int len, char char = ' ')
        #{
        #    str ret;
        #    int l = len - strlen(s);
        #
        #    if(l<=0)
        #        return s;
        #
        #    ret = s + strrep(char, l);
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #padLeft
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет указанный символ слева до нужной длины
        #// если строка уже длиннее, то ничего не делает. Поэтому результат может быть длиннее указанной длины.
        #static str padLeft(str s, int len, char char = ' ')
        #{
        #    str ret;
        #    int l = len - strlen(s);
        #
        #    if(l<=0)
        #        return s;
        #
        #    ret = strrep(char, l) + s;
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #padRight
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет указанный символ справа до нужной длины
        #// если строка уже длиннее, то ничего не делает. Поэтому результат может быть длиннее указанной длины.
        #static str padRight(str s, int len, char char = ' ')
        #{
        #    str ret;
        #    int l = len - strlen(s);
        #
        #    if(l<=0)
        #        return s;
        #
        #    ret = s + strrep(char, l);
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #padStart
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет указанный символ слева до нужной длины
        #// если строка уже длиннее, то ничего не делает. Поэтому результат может быть длиннее указанной длины.
        #//
        #// по идее, различие между padLeft и padStart должно проявляться на текстах с разным направлением письма
        #// но в аксапте никаких различий нет
        #static str padStart(str s, int len, char char = ' ')
        #{
        #    str ret;
        #    int l = len - strlen(s);
        #
        #    if(l<=0)
        #        return s;
        #
        #    ret = strrep(char, l) + s;
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #split
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// делит строку на подстроки, разделенные строкой delimiterStr
        #// возвращает контейнер подстрок. Если maxSubstrings не ноль, то максимальная контейнера не будет превышать maxSubstrings.
        #// возвращает контейнер, содержащий исходную строку, если ни одного символа-разделителя не найдено.
        #//
        #// пример:
        #//      strSplitCon('ABC:=DEF:=GH',':=')   == ['ABC','DEF','GH']
        #//      strSplitCon('ABC:=DEF:=GH',':=',2) == ['ABC','DEF:=GH']
        #//      strSplitCon('ABC:=DEF:=GH',':=',1) == ['ABC:=DEF:=GH']
        #//
        #static container split(str s, str delimiterStr, int maxSubstrings = 0)
        #{
        #    container ret;
        #    int delimiterLen;
        #    int len;
        #    int posFrom = 1;
        #    int pos;
        #
        #    if( delimiterStr == '' )
        #        return [s];
        #
        #    delimiterLen = strlen(delimiterStr);
        #    len = strlen(s);
        #    do
        #    {
        #        maxSubstrings--;
        #        if( maxSubstrings == 0 )
        #            break;
        #
        #        pos = strscan(s, delimiterStr, posFrom, len);
        #        if( pos == 0 )
        #            break;
        #
        #        ret += substr(s, posFrom, pos-posFrom);
        #        posFrom = pos+delimiterLen;
        #    }
        #    while( maxSubstrings );
        #
        #    ret += substr(s, posFrom, len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #splitChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// делит строку на подстроки, разделенные хотя бы одним символом из delimiters (по-умолчанию это пробел, табуляция и перевод строки - whitespaces)
        #// возвращает контейнер подстрок.
        #// возвращает контейнер, содержащий исходную строку, если ни одного символа-разделителя не найдено.
        #// Если maxSubstrings не ноль, то максимальная контейнера не будет превышать maxSubstrings.
        #//
        #// пример:
        #//      strSplitChar('ABC DEF GH')        == ['ABC','DEF','GH']
        #//      strSplitChar('ABC-DEF-GH','-=')   == ['ABC','DEF','GH']
        #//      strSplitChar('ABC-DEF-GH','-=',2) == ['ABC','DEF-GH']
        #//      strSplitChar('ABC-DEF-GH','-=',1) == ['ABC-DEF-GH']
        #//
        #static container splitChar(str s, str delimiters = ' \t\n', int maxSubstrings = 0)
        #{
        #    container ret;
        #    int len;
        #    int posFrom = 1;
        #    int pos;
        #
        #    if( delimiters == '' )
        #        return [s];
        #
        #    len = strlen(s);
        #    do
        #    {
        #        maxSubstrings--;
        #        if( maxSubstrings == 0 )
        #            break;
        #
        #        pos = strfind(s, delimiters, posFrom, len);
        #        if( pos == 0 )
        #            break;
        #
        #        ret += substr(s, posFrom, pos-posFrom);
        #        posFrom = pos+1;
        #    }
        #    while( maxSubstrings );
        #
        #    ret += substr(s, posFrom, len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #splitCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// делит строку на подстроки, разделенные хотя бы одной строкой из контейнера delimiterCon (по умолчанию пробел, табуляция и переводы строк - whitespaces)
        #// результат не зависит от порядка разделителей в контейнере.
        #// возвращает контейнер подстрок. Если maxSubstrings не ноль, то максимальная контейнера не будет превышать maxSubstrings.
        #// возвращает контейнер, содержащий исходную строку, если ни одного символа-разделителя не найдено.
        #//
        #// пример:
        #//      strSplitCon('ABC=DEF:=GH',[':=','='])   == ['ABC','DEF','GH']
        #//      strSplitCon('ABC=DEF:=GH',[':=','='],2) == ['ABC','DEF:=GH']
        #//      strSplitCon('ABC=DEF:=GH',[':=','='],1) == ['ABC=DEF:=GH']
        #//
        #static container splitCon(str s, container delimiterCon = [' ','\t','\n','\r\n','\n\r'], int maxSubstrings = 0)
        #{
        #    container ret;
        #    int d;
        #    str delimiter;
        #    str delimiterStr;
        #    int delimiterPos;
        #    int delimiterLen;
        #    int len;
        #    int posFrom = 1;
        #    int pos;
        #
        #    if( delimiterCon == connull() )
        #        return [s];
        #
        #    len = strlen(s);
        #    do
        #    {
        #        maxSubstrings--;
        #        if( maxSubstrings == 0 )
        #            break;
        #
        #        delimiter = conpeek(delimiterCon, 1);       // проверка выше гарантирует, что хотя бы один разделитель у нас есть
        #        pos = strscan(s, delimiter, posFrom, len);  // позиция первого разделителя
        #        for(d = 2; d <= conlen(delimiterCon); d++)  // позиции разделителей после первого
        #        {
        #            delimiterStr = conpeek(delimiterCon, d);
        #            delimiterPos = strscan(s, delimiterStr, posFrom, len);
        #            if( delimiterPos == 0 )
        #                continue;
        #
        #            if( pos && pos < delimiterPos )
        #                continue;
        #
        #            if( pos == delimiterPos && strlen(delimiter) >= strlen(delimiterStr) )
        #                continue;
        #
        #            // берем разделитель с минимальной позицией
        #            // если позиция совпадает, то берем разделитель с максимальной длиной
        #            delimiter = delimiterStr;
        #            pos = delimiterPos;
        #        }
        #        if( pos == 0 )  // разделители не найдены
        #            break;
        #
        #        ret += substr(s, posFrom, pos-posFrom);
        #        posFrom = pos+strlen(delimiter);
        #    }
        #    while( maxSubstrings );
        #
        #    ret += substr(s, posFrom, len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #start
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку заданной длины слева
        #//
        #// по идее, различие между strLeft и start должно проявляться на текстах с разным направлением письма
        #// но в аксапте никаких различий нет
        #//
        #static str start(str s, int len)
        #{
        #    str ret;
        #
        #    if( len <= 0 )
        #        return '';
        #
        #    ret = substr(s, 1, len);
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #strLeft
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку заданной длины слева
        #//
        #// в X++ есть ключевое слово left. Поэтому обявить метод с именем left нельзя
        #static str strLeft(str s, int len)
        #{
        #    str ret;
        #
        #    if( len <= 0 )
        #        return '';
        #
        #    ret = substr(s, 1, len);
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #strRight
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку заданной длины справа
        #static str strRight(str s, int len)
        #{
        #    str ret;
        #
        #    if( len<=0 )
        #        return '';
        #
        #    ret = substr(s, strlen(s), -len);
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #take
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает подстроку заданной длины с указанного символа
        #// len может быть отрицательным числом.
        #static str take(str s, int len, int startFrom = 1)
        #{
        #    str ret = substr(s, startFrom, len);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trim
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// убирает указанные символы в начале и в конце строки
        #// по умолчанию, убирает пробелы, табуляцию и переводы строк
        #static str trim(str s, str ws = ' \t\r\n')
        #{
        #    str ret;
        #    int len = strlen(s);
        #    int fromPos = strnfind(s, ws, 1, len);
        #    int toPos = strnfind(s, ws, len, -len);
        #
        #    if( fromPos && fromPos <= toPos )
        #    {
        #        ret = substr(s, fromPos, toPos - fromPos + 1);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trimEnd
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #//TODO
        #public static str trimEnd(str s, str ws = ' \t\r\n')
        #{
        #    str ret;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trimIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// удаляет отступ, который применим для каждой строки в многострочном тексте
        #// табуляцию рассматривает как непробельный значащий символ
        #// работает как trimIndent в Kotlin, https://kotlinlang.org/api/latest/jvm/stdlib/kotlin.text/trim-indent.html
        #// см. также https://openjdk.java.net/jeps/355
        #public static str trimIndent(str s)
        #{
        #    return TextUtil::trimIndent(s);
        #}
      ENDSOURCE
      SOURCE #trimStart
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #//TODO
        #public static str trimStart(str s, str ws = ' \t\r\n')
        #{
        #    str ret;
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: TextBufferUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #TextBufferUtil
    PROPERTIES
      Name                #TextBufferUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class TextBufferUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #append
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer append(TextBuffer textOrNull, str s)
        #{
        #    TextBuffer text = TextBufferUtil::safeNotNull(textOrNull);
        #
        #    if( s )
        #    {
        #        text.appendText(s);
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #appendCallStack
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// отображаем как в отладчике
        #public static TextBuffer appendCallStack(TextBuffer text, int skip = 1, container callStack = xSession::xppCallStack())
        #{
        #    int len   = conLen(callStack);
        #    int start = max(2, skip*2+2);
        #    int i;
        #
        #    if( !text )
        #    {
        #        text = new TextBuffer();
        #    }
        #
        #    for( i=start; i<=len; i+=2 )
        #    {
        #        text.appendText(strfmt('\n%1 - line %2', conPeek(callStack, i-1), conPeek(callStack, i)));
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #appendContainer
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer appendContainer(TextBuffer textOrNull, container con, str sep = ',')
        #{
        #    ConEnumerator ce = ConEnumerator::construct(con);
        #    TextBuffer text = TextBufferUtil::appendEnumerator(textOrNull, ce, sep);
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #appendEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer appendEnumerator(TextBuffer textOrNull, Enumerator e, str sep = ',')
        #{
        #    TextBuffer text = TextBufferUtil::safeNotNull(textOrNull);
        #    boolean tail;
        #
        #    while( e && e.moveNext() )
        #    {
        #        if( tail && sep )
        #        {
        #            text.appendText(sep);
        #        }
        #
        #        text.appendText(strFmt('%1', e.current()));
        #
        #        tail = true;
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #appendIfNotEmpty
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer appendIfNotEmpty(TextBuffer text, str s)
        #{
        #    if( s && text && text.size() )
        #    {
        #        text.appendText(s);
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #appendInfolog
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer appendInfolog(TextBuffer textOrNull, InfologData info = infolog.export(), str newLine = '\n')
        #{
        #    TextBuffer              text = TextBufferUtil::safeNotNull(textOrNull);
        #    boolean                 firstLine = true;
        #    SysInfologEnumerator    sysInfologEnumerator;
        #    SysInfologMessageStruct messageStruct;
        #    SysInfoLogStr           message;
        #
        #    if (info)
        #    {
        #        sysInfologEnumerator = SysInfologEnumerator::newData(info);
        #        while (sysInfologEnumerator.moveNext())
        #        {
        #            if (!firstLine && newLine)
        #            {
        #                text.appendText(newline);
        #            }
        #
        #            messageStruct = SysInfologMessageStruct::construct(sysInfologEnumerator.currentMessage());
        #            message = messageStruct.message();
        #            if (message)
        #            {
        #                text.appendText(message);
        #                firstLine = false;
        #            }
        #        }
        #    }
        #
        #    return text;
        #}
        #
      ENDSOURCE
      SOURCE #calcIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// рассчитывает бощий отступ, который может быть применим для каждой строки в многострочном тексте
        #// табуляцию рассматривает как непробельный значащий символ
        #// при расчете игнорирует пустые и пробельные строки (строки, содержащие только пробелы)
        #public static int calcIndent(TextBuffer text)
        #{
        #    int     ident;
        #    int     lineIndent;
        #    int     startPos    = 1;
        #
        #    if( !text )
        #        return 0;
        #
        #    if( !text.size() )
        #        return 0;
        #
        #    ident = maxint();
        #
        #    ///////////////////
        #    if( text.find('< +') )
        #    {
        #        lineIndent = text.matchLen();
        #        startPos = text.matchPos() + text.matchLen();
        #    }
        #
        #    while( text.find('\n *', startPos) )
        #    {
        #        // вторая половина условия срабатывает толкьо для непробельных строк
        #        if( lineIndent < ident && startPos != text.matchPos() )
        #        {
        #            ident = lineIndent;
        #        }
        #
        #        lineIndent = text.matchLen() - 1;
        #        startPos = text.matchPos() + text.matchLen();
        #    }
        #
        #    if( lineIndent < ident && startPos <= text.size() )
        #    {
        #        ident = lineIndent;
        #    }
        #    else if( ident == maxint() )
        #    {
        #        ident = lineIndent;
        #    }
        #
        #    return ident;
        #}
      ENDSOURCE
      SOURCE #changeIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет или удаляет отступ слева у каждой строки в многострочном тексте
        #// пробелы справа каждой строки оставляет как есть
        #// табуляцию рассматривает как непробельный значащий символ
        #// возвращает измененный TextBuffer, не создает копию если TextBuffer уже создан
        #// возвращаемый объект никогда не равен null
        #//
        #// text - исходный текст, в котором метод изменяет отступы
        #// identOffset - положительное значение увеличивает отступ, отрицательное значение уменьшает отступ, нулевое значение не меняет текст
        #//
        #public static TextBuffer changeIndent(TextBuffer textOrNull, int indentOffset)
        #{
        #    TextBuffer text = TextBufferUtil::safeNotNull(textOrNull);
        #
        #    if( text.size() == 0 )
        #        return text;
        #
        #    while( indentOffset < 0 )
        #    {
        #        text.replace('< ', '');
        #        text.replace('\n ', '\n');
        #        indentOffset++;
        #    }
        #
        #    while( indentOffset > 0 )
        #    {
        #        text.insert(' ', 1);
        #        text.replace('\n', '\n ');
        #        indentOffset--;
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #getText
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str getText(TextBuffer text)
        #{
        #    str ret = text ? text.getText() : '';
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #newFrom
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer newFrom(TextBuffer textOrNull)
        #{
        #    TextBuffer text = new TextBuffer();
        #
        #    if( textOrNull && textOrNull.size() )
        #    {
        #        text.setText(textOrNull.getText());
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #newFromStr
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer newFromStr(str s)
        #{
        #    TextBuffer text = new TextBuffer();
        #
        #    if( s )
        #    {
        #        text.appendText(s);
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #safeNotNull
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static TextBuffer safeNotNull(TextBuffer text)
        #{
        #    if( !text )
        #    {
        #        text = new TextBuffer();
        #    }
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #trimIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// удаляет общий для всех строк отступ в многострочном тексте
        #// пробелы справа каждой строки оставляет как есть
        #// табуляцию рассматривает как непробельный значащий символ
        #//
        #// работает как trimIndent в Kotlin, https://kotlinlang.org/api/latest/jvm/stdlib/kotlin.text/trim-indent.html
        #// см. также https://openjdk.java.net/jeps/355
        #public static TextBuffer trimIndent(TextBuffer text)
        #{
        #    TextBuffer  ret     = new TextBuffer();
        #    int         startPos;
        #    int         endPos;
        #    int         len;
        #    int         indent;
        #;
        #    if( text == null )
        #        return ret;
        #
        #    if( text.size() == 0 )
        #        return ret;
        #
        #    // удаляем первую пустую или пробельную строку
        #    if( text.find('< *\n') )
        #        startPos = text.matchPos() + text.matchLen();
        #    else
        #        startPos = 1;
        #
        #    // удаляем последнюю пустую или пробельную строку
        #    if( text.find('\n *>') )
        #        endPos = text.matchPos() - 1;
        #    else
        #        endPos = text.size();
        #
        #    len = endPos - startPos + 1;
        #
        #    if( len <= 0 )
        #        return ret;
        #
        #    if( len == text.size() )
        #        ret = text;
        #    else
        #        ret.setText(text.subStr(startPos, len));
        #
        #    indent = TextBufferUtil::calcIndent(ret);
        #
        #    if( indent == 0 )
        #        return ret;
        #
        #    ret = TextBufferUtil::changeIndent(ret, -indent);
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: TextUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #TextUtil
    PROPERTIES
      Name                #TextUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// класс оперирует с текстом - многострочной строкой (multiline string)
        #// если текст содержит только одну строку, то класс работает также как и строковые функции
        #class TextUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #after
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находится после первого вхожения подстроки
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::after('ABC-DEF-GH\nIJK-LMN-OP','-') == 'DEF-GH\nLMN-OP'
        #//      TextUtil::after('ABC-DEF-GH\nIJK-LMN-OP',',') == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str after(str text, str search)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( search == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)^.*?%1', search);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #afterChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после первого вхожения любого из указанных символов characters
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::afterChar('ABC-DEF-GH\nIJK-LMN-OP','-') == 'DEF-GH\nLMN-OP'
        #//      TextUtil::afterChar('ABC-DEF-GH\nIJK-LMN-OP',',') == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str afterChar(str text, str characters)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( characters == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)^.*?[%1]', characters);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #afterLast
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после последнего вхожения подстроки
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::afterLast('ABC-DEF-GH\nIJK-LMN-OP','-') == 'GH\nOP'
        #//      TextUtil::afterLast('ABC-DEF-GH\nIJK-LMN-OP',',') == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str afterLast(str text, str search)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( search == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)^.*%1', search);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #afterLastChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после последнего вхожения любого из указанных символов characters
        #// возвращает исходную строку, если ни одного вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::afterLastChar('ABC-DEF-GH\nIJK-LMN-OP','-=') == 'GH\nOP'
        #//      TextUtil::afterLastChar('ABC-DEF-GH\nIJK-LMN-OP',',')  == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str afterLastChar(str s, str characters)
        #{
        #    str pattern;
        #
        #    if( s == '' )
        #        return s;
        #
        #    if( characters == '' )
        #        return s;
        #
        #    pattern = strfmt(@'(?m)^.*[%1]', characters);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    s = System.Text.RegularExpressions.Regex::Replace(s, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return s;
        #}
      ENDSOURCE
      SOURCE #appendEnd
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет строку в конец каждой подстроки текста
        #//
        #// пример:
        #//      TextUtil::appendEnd('ABC-DEF-GH\nIJK-LMN-OP',';') == 'ABC-DEF-GH;\nIJK-LMN-OP;'
        #//      TextUtil::appendEnd('ABC-DEF-GH',            ';') == 'ABC-DEF-GH;'
        #//      TextUtil::appendEnd('',                      ';') == ';'
        #//
        #public static str appendEnd(str text, str start)
        #{
        #    if( text == '' )
        #        return start;
        #
        #    if( start == '' )
        #        return text;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, @'(?m)$', start);
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #appendStart
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет строку в начало каждой подстроки текста
        #//
        #// пример:
        #//      TextUtil::appendStart('ABC-DEF-GH\nIJK-LMN-OP','* ') == '* ABC-DEF-GH\n* IJK-LMN-OP'
        #//      TextUtil::appendStart('ABC-DEF-GH',            '* ') == '* ABC-DEF-GH'
        #//      TextUtil::appendStart('',                      '* ') == '* '
        #//
        #public static str appendStart(str text, str end)
        #{
        #    if( text == '' )
        #        return end;
        #
        #    if( end == '' )
        #        return text;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, @'(?m)^', end);
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #before
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после первого вхожения подстроки
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::before('ABC-DEF-GH\nIJK-LMN-OP','-') == 'ABC\nIJK'
        #//      TextUtil::before('ABC-DEF-GH\nIJK-LMN-OP',',') == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str before(str text, str search)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( search == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)%1.*?$', search);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #beforeChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после первого вхожения любого из указанных символов characters
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::beforeChar('ABC-DEF-GH\nIJK-LMN-OP','=-') == 'ABC\nIJK'
        #//      TextUtil::beforeChar('ABC-DEF-GH\nIJK-LMN-OP',',') == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str beforeChar(str text, str characters)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( characters == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)[%1].*?$', characters);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #beforeLast
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после последнего вхожения подстроки
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::beforeLast('ABC-DEF-GH\nIJK-LMN-OP','-') == 'ABC-DEF\nIJK-LMN'
        #//      TextUtil::beforeLast('ABC-DEF-GH\nIJK-LMN-OP',',') == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str beforeLast(str text, str search)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( search == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)%1(?!.*%1).*$', search);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #beforeLastChar
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст с подстроками, которые находятся после последнего вхожения любого из указанных символов characters
        #// возвращает исходный текст, если вхождения не найдено
        #//
        #// пример:
        #//      TextUtil::beforeLastChar('ABC-DEF-GH\nIJK-LMN-OP','=-') == 'ABC-DEF\nIJK-LMN'
        #//      TextUtil::beforeLastChar('ABC-DEF-GH\nIJK-LMN-OP',',')  == 'ABC-DEF-GH\nIJK-LMN-OP'
        #//
        #static str beforeLastChar(str text, str characters)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( characters == '' )
        #        return text;
        #
        #    pattern = strfmt(@'(?m)[%1](?!.*[%1]).*$', characters);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #calcIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// рассчитывает отступ, который применим для каждой строки в многострочном тексте
        #// табуляцию рассматривает как один непробельный значащий символ
        #public static int calcIndent(str text)
        #{
        #    int indent = maxint();
        #    int i;
        #
        #    System.Text.RegularExpressions.Match            rMatch;
        #    System.Text.RegularExpressions.GroupCollection  rGroupCollection;
        #    System.Text.RegularExpressions.Group            rGroup;
        #
        #    if( text == '' )
        #        return 0;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    rMatch = System.Text.RegularExpressions.Regex::Match(text, @'^(\r?\n)*(?<indent> *).*((\r?\n)+( *|\k<indent>.*))*$');
        #    if( rMatch && rMatch.get_Success() )
        #    {
        #        rGroupCollection = rMatch.get_Groups();
        #        rGroup = rGroupCollection.get_Item('indent');
        #        i = rGroup.get_Length();
        #        indent = min(indent, i);
        #    }
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    if( indent == maxint() )
        #        return 0;
        #
        #    return indent;
        #}
      ENDSOURCE
      SOURCE #callStack
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// отображаем как в отладчике
        #public static str callStack(int skip = 2, container callStack = xSession::xppCallStack())
        #{
        #    str ret = TextBufferUtil::appendCallStack(null, skip, callStack).getText();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #changeIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет или удаляет отступ слева у каждой строки в многострочном тексте
        #// пробелы справа каждой строки оставляет как есть
        #// табуляцию рассматривает как непробельный значащий символ
        #//
        #// text - исходный текст, в котором метод изменяет отступы
        #// identOffset - положительное значение увеличивает отступ, отрицательное значение уменьшает отступ, нулевое значение не меняет текст
        #//
        #public static str changeIndent(str text, int indentOffset)
        #{
        #    str pattern;
        #    str replacement;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( indentOffset == 0 )
        #        return text;
        #
        #    if( indentOffset < 0 )
        #    {
        #        pattern = strfmt('(?m)^ {1,%1}', -indentOffset);
        #        replacement = '';
        #    }
        #    else
        #    {
        #        pattern = '(?m)^';
        #        replacement = strrep(' ', indentOffset);
        #    }
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, replacement);
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #drop
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// в каждой строке многострочного текста:
        #// = удаляет первые n символов, если n > 0
        #// = удаляет последние n символов, если n < 0
        #// возвращает исходный текст, если n == 0
        #public static str drop(str text, int n)
        #{
        #    if( n > 0 )
        #        return TextUtil::dropStart(text, n);
        #
        #    return TextUtil::dropEnd(text, -n);
        #}
      ENDSOURCE
      SOURCE #dropEnd
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// в каждой строке многострочного текста:
        #// = удаляет последние n символов, если n < 0
        #// возвращает исходный текст, если n => 0
        #public static str dropEnd(str text, int n)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( n >= 0 )
        #        return text;
        #
        #    pattern = strfmt('(?m).{1,%1}$', -n);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #dropStart
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// в каждой строке многострочного текста:
        #// удаляет первые n символов, если n > 0
        #// возвращает исходный текст, если n <= 0
        #public static str dropStart(str text, int n)
        #{
        #    str pattern;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( n <= 0 )
        #        return text;
        #
        #    pattern = strfmt('(?m)^.{1,%1}', n);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    text = System.Text.RegularExpressions.Regex::Replace(text, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return text;
        #}
      ENDSOURCE
      SOURCE #infolog
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static str infolog(InfologData info = infolog.export(), str newLine = '\n')
        #{
        #    str ret = TextBufferUtil::appendInfolog(null, info, newLine).getText();
        #
        #    return ret;
        #}
        #
      ENDSOURCE
      SOURCE #line
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает n-ую строку из многострочного текста
        #public static str line(str s, int n)
        #{
        #    str ret = strline(s, n);
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #lines
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static System.String[] lines(str text)
        #{
        #    System.String[] ret;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    ret = System.Text.RegularExpressions.Regex::Split(text, '\n');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #padLeft
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет указанный символ слева до нужной длины
        #// если строка уже длиннее, то ничего не делает. Поэтому результат может быть длиннее указанной длины.
        #public static str padLeft(str text, int length, char char = ' ')
        #{
        #    TextBuffer                              ret = new TextBuffer();
        #    str                                     s;
        #    int                                     n;
        #    System.Char                             c;
        #    System.String                           ss;
        #    System.Text.RegularExpressions.Match    rMatch;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( char == '' )
        #        return text;
        #
        #    if( length <= 0 )
        #        return text;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    c = System.Char::Parse(char);
        #
        #    rMatch = System.Text.RegularExpressions.Regex::Match(text, @'(?m)^.*$');
        #    while( rMatch && rMatch.get_Success() )
        #    {
        #        ss = rMatch.get_Value();
        #        s = ss.PadLeft(length, c);
        #
        #        if( n )
        #            ret.appendText('\n');
        #
        #        ret.appendText(s);
        #
        #        rMatch = rMatch.NextMatch();
        #        ++n;
        #    }
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return ret.getText();
        #}
      ENDSOURCE
      SOURCE #padRight
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// добавляет указанный символ справа до нужной длины
        #// если строка уже длиннее, то ничего не делает. Поэтому результат может быть длиннее указанной длины.
        #static str padRight(str text, int length, char char = ' ')
        #{
        #    TextBuffer  ret = new TextBuffer();
        #    str         s;
        #
        #    System.Char                     c;
        #    System.String                   ss;
        #    System.String[]                 ssa;
        #    System.Collections.IEnumerator  se;
        #
        #    if( text == '' )
        #        return text;
        #
        #    if( char == '' )
        #        return text;
        #
        #    if( length <= 0 )
        #        return text;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    ssa = TextUtil::lines(text);
        #    se  = ssa.GetEnumerator();
        #    while( se.MoveNext() )
        #    {
        #        if( ret.size() )
        #        {
        #            ret.appendText('\n');
        #        }
        #        else
        #        {
        #            c = System.Char::Parse(char);
        #        }
        #
        #        ss = se.get_Current();
        #        s  = ss.PadRight(length, c);
        #
        #        ret.appendText(s);
        #    }
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return ret.getText();
        #}
      ENDSOURCE
      SOURCE #strLeft
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст, в котором каждая строка содержит не более заданной длины справа
        #static str strLeft(str s, int len)
        #{
        #    str pattern;
        #
        #    if( s == '' )
        #        return s;
        #
        #    if( len > 0 )
        #        pattern = strfmt(@'(?m)(?<=.{%1}).+', len);
        #    else
        #        pattern = strfmt(@'(?m).+', len);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    s = System.Text.RegularExpressions.Regex::Replace(s, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return s;
        #}
      ENDSOURCE
      SOURCE #strRight
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает текст, в котором каждая строка содержит не более заданной длины слева
        #static str strRight(str s, int len)
        #{
        #    str pattern;
        #
        #    if( s == '' )
        #        return s;
        #
        #    if( len > 0 )
        #        pattern = strfmt(@'(?m).+(?=.{%1})', len);
        #    else
        #        pattern = strfmt(@'(?m).+', len);
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    s = System.Text.RegularExpressions.Regex::Replace(s, pattern, '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    return s;
        #}
      ENDSOURCE
      SOURCE #toArray
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// TODO
        #public static container toArray(str s)
        #{
        #    container ret = StrUtil::splitChar(s, '\n');
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toCon
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static container toCon(str s)
        #{
        #    container ret = StrUtil::splitChar(s, '\n');
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toList
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// TODO
        #public static container toList(str s)
        #{
        #    container ret = StrUtil::splitChar(s, '\n');
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toMap
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// TODO
        #public static container toMap(str s, int startLineNum = 1)
        #{
        #    container ret = StrUtil::splitChar(s, '\n');
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #toSet
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// TODO
        #public static container toSet(str s)
        #{
        #    container ret = StrUtil::splitChar(s, '\n');
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trim
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// убирает указанные символы в начале и в конце всех строк в многострочном тексте
        #// по умолчанию, убирает пробелы, табуляцию и переводы строк
        #static str trim(str s, str ws = ' \t')
        #{
        #    str ret;
        #    int len = strlen(s);
        #    int fromPos = strnfind(s, ws, 1, len);
        #    int toPos = strnfind(s, ws, len, -len);
        #
        #    if( fromPos && fromPos <= toPos )
        #    {
        #        ret = substr(s, fromPos, toPos - fromPos + 1);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trimBlankLines
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static str trimBlankLines(str s, str ws = ' \t\r\n')
        #{
        #    str ret;
        #    //TODO
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trimEnd
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #//TODO
        #public static str trimEnd(str s, str ws = ' \t')
        #{
        #    str ret;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #trimIndent
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// удаляет отступ, который применим для каждой строки в многострочном тексте
        #// табуляцию рассматривает как непробельный значащий символ
        #// работает как trimIndent в Kotlin, https://kotlinlang.org/api/latest/jvm/stdlib/kotlin.text/trim-indent.html
        #// см. также https://openjdk.java.net/jeps/355
        #public static str trimIndent(str s)
        #{
        #    int indent;
        #
        #    if( s == '' )
        #        return s;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    // удаляем первую и последнюю пустые или пробельные строки
        #    s = System.Text.RegularExpressions.Regex::Replace(s, '^ *\n|\n *$', '');
        #
        #    CodeAccessPermission::revertAssert();
        #
        #    indent = TextUtil::calcIndent(s);
        #
        #    s = TextUtil::changeIndent(s, -indent);
        #
        #    return s;
        #}
      ENDSOURCE
      SOURCE #trimStart
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #//TODO
        #public static str trimStart(str s, str ws = ' \t')
        #{
        #    str ret;
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SessionUtil unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SessionUtil
    PROPERTIES
      Name                #SessionUtil
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class SessionUtil
        #{
        #}
      ENDSOURCE
      SOURCE #new
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// это Util-класс, содержащий только статические методы
        #// нет смысла создавать объекты этого класса
        #private void new()
        #{
        #}
      ENDSOURCE
      SOURCE #clientSessionTable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static server SysClientSessions clientSessionTable(int _sessionId = 0)
        #{
        #    int sessId = _sessionId ? _sessionId : sessionid();
        #
        #    SysClientSessions clientSessions;
        #    ;
        #
        #    new SkipAOSValidationPermission().assert();
        #
        #    //BP deviation documented
        #    clientSessions.skipAosValidation(true);
        #
        #    select clientSessions
        #    where clientSessions.SessionId == sessId;
        #
        #    return clientSessions;
        #}
      ENDSOURCE
      SOURCE #clientType
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static ClientType clientType(int _sessionId = 0)
        #{
        #    return SessionUtil::session().clientKind();
        #
        #}
      ENDSOURCE
      SOURCE #isInRoleAdmin
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true если:
        #// пользователь наделен правами локального администратора
        #// и текущая сессия была запущена runAsAdministrator
        #//
        #public static boolean isInRoleAdmin()
        #{
        #    #define.CacheOwner('SessionFlags')
        #    #define.CacheKey('isInRoleAdmin')
        #
        #    boolean ret;
        #    anytype cached = classfactory.globalCache().get(#CacheOwner, #CacheKey, '');
        #
        #    if( typeof(cached) == Types::Integer )
        #    {
        #        ret = cached;
        #    }
        #    else
        #    {
        #        ret = SessionUtil::isInRoleAdmin_Slow();
        #
        #        classfactory.globalCache().set(#CacheOwner, #CacheKey, ret);
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #isInRoleAdmin_Slow
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает true если:
        #// пользователь наделен правами локального администратора
        #// и текущая сессия была запущена runAsAdministrator
        #//
        #// используйте метод isInRoleAdmin(), в котором выполняется кэширование
        #//
        #public static boolean isInRoleAdmin_Slow()
        #{
        #    anytype perm = new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    System.Security.Principal.WindowsIdentity    id = System.Security.Principal.WindowsIdentity::GetCurrent();
        #    System.Security.Principal.WindowsPrincipal   principal = new System.Security.Principal.WindowsPrincipal(id);
        #    System.Security.Principal.WindowsBuiltInRole admin = System.Security.Principal.WindowsBuiltInRole::Administrator;
        #
        #    boolean ret = principal.IsInRole(admin);
        #    ;
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #serverSessionTable
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static server SysServerSessions serverSessionTable(int _sessionId = 0)
        #{
        #    int sessId = _sessionId ? _sessionId : sessionid();
        #
        #    SysClientSessions clientSessions;
        #    SysServerSessions serverSessions;
        #    ;
        #
        #    new SkipAOSValidationPermission().assert();
        #
        #    //BP deviation documented
        #    clientSessions.skipAosValidation(true);
        #    //BP deviation documented
        #    serverSessions.skipAosValidation(true);
        #
        #    select serverSessions
        #    exists join clientSessions
        #    where clientSessions.ServerId == serverSessions.ServerId
        #       && clientSessions.SessionId == sessId;
        #
        #    return serverSessions;
        #}
      ENDSOURCE
      SOURCE #session
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Session session(int _sessionId = 0, boolean _checkSession = false)
        #{
        #    int sessId = _sessionId ? _sessionId : sessionid();
        #
        #    Session ret = new Session(sessId, _checkSession);
        #
        #    return ret;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysDictClass unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysDictClass
    PROPERTIES
      Name                #SysDictClass
      Extends             #DictClass
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #as
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static public Object as(Object _class, classId _classId)
        #{
        #    if (_class && SysDictClass::is(_class, _classId))
        #        return _class;
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysDictClass construct(ClassId classId)
        #{
        #    return new SysDictClass(classId);
        #}
      ENDSOURCE
      SOURCE #is
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean is(Object _class, classId _classId)
        #{
        #    boolean ret = false;
        #
        #    if( _classId )
        #    {
        #        ret = SysDictClass::isEqualOrSuperclass(classidget(_class), _classId);
        #
        #        if( !ret )
        #        {
        #            ret = new SysDictClass(classidget(_class)).isImplementing(_classId);
        #        }
        #    }
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #subClassListEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static ListEnumerator subClassListEnumerator(ClassId classId)
        #{
        #    DictClass        dc =   new DictClass(classId);
        #    List             list = dc ? dc.extendedBy() : null;
        #    ListEnumerator   le   = list ? list.getEnumerator() : null;
        #
        #    return le;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysDictEnum unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysDictEnum
    PROPERTIES
      Name                #SysDictEnum
      Extends             #DictEnum
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #newFromEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysDictEnum newFromEnum(anytype enum)
        #{
        #    enumId          enumId          = DictEnum::value2id(enum);
        #    SysDictEnum     sysDictEnum     = new SysDictEnum(enumId);
        #
        #    return sysDictEnum;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysDictTable unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysDictTable
    PROPERTIES
      Name                #SysDictTable
      Extends             #DictTable
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #fieldsWithEDT
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Set fieldsWithEDT(ExtendedTypeId edt)
        #{
        #    int cnt;
        #    int i;
        #    int j;
        #    tableId tableId = this.id();
        #    SysDictField sysDictField;
        #    SysDictField sysDictFieldArray;
        #    Set set = new Set(Types::Class);
        #
        #    if (!edt)
        #    {
        #        return set;
        #    }
        #
        #    //
        #    // Include fields
        #    //
        #    cnt = this.fieldCnt();
        #    for (i=1; i<=cnt; i++)
        #    {
        #        sysDictField = new SysDictField(tableId, this.fieldCnt2Id(i));
        #
        #        //
        #        // Skip fields not stored in the database
        #        // This is typical system fields, like ModifiedBy, CreatedBy, etc.
        #        //
        #        if (sysDictField && sysDictField.saveContents() && SysDictType::isEqualOrExtending(sysDictField.extendedTypeId(), edt))
        #        {
        #            for (j=1; j<=sysDictField.arraySize(); j++)
        #            {
        #                sysDictFieldArray = new SysDictField(tableId, this.fieldCnt2Id(i), j);
        #                set.add(sysDictFieldArray);
        #            }
        #        }
        #    }
        #    return set;
        #}
      ENDSOURCE
      SOURCE #fieldsWithEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public Set fieldsWithEnum(EnumId enumId)
        #{
        #    int cnt;
        #    int i;
        #    int j;
        #    tableId tableId = this.id();
        #    SysDictField sysDictField;
        #    SysDictField sysDictFieldArray;
        #    Set set = new Set(Types::Class);
        #
        #    if (!enumId)
        #    {
        #        return set;
        #    }
        #
        #    //
        #    // Include fields
        #    //
        #    cnt = this.fieldCnt();
        #    for (i=1; i<=cnt; i++)
        #    {
        #        sysDictField = new SysDictField(tableId, this.fieldCnt2Id(i));
        #
        #        //
        #        // Skip fields not stored in the database
        #        // This is typical system fields, like ModifiedBy, CreatedBy, etc.
        #        //
        #        if (sysDictField && sysDictField.saveContents() && sysDictField.enumId() == enumId)
        #        {
        #            for (j=1; j<=sysDictField.arraySize(); j++)
        #            {
        #                sysDictFieldArray = new SysDictField(tableId, this.fieldCnt2Id(i), j);
        #                set.add(sysDictFieldArray);
        #            }
        #        }
        #    }
        #    return set;
        #}
      ENDSOURCE
      SOURCE #sysDictFieldGroup
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysDictFieldGroup sysDictFieldGroup(str _fieldGroupName)
        #{
        #    int i;
        #    ;
        #
        #    for(i = 1; i <= this.fieldGroupCnt(); i++)
        #        if( this.fieldGroup(i) == _fieldGroupName )
        #            return new SysDictFieldGroup(this.id(), _fieldGroupName);
        #
        #    return null;
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toString()
        #{
        #    return strFmt('%1 [%2]', this.name(), this.id());
        #}
      ENDSOURCE
      SOURCE #as
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Common as(Common rec, TableId tableOrMapId)
        #{
        #    if( SysDictTable::is(rec, tableOrMapId) )
        #        return rec;
        #
        #    return RecordUtil::commonRecord();
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysDictTable construct(tableId _tableId)
        #{
        #    SysDictTable sysDictTable = new SysDictTable(_tableId);
        #
        #    return sysDictTable;
        #}
        #
      ENDSOURCE
      SOURCE #fieldEnumerator
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SetEnumerator fieldEnumerator(tableId tableId)
        #{
        #    SysDictTable    dt      = new SysDictTable(tableId);
        #    SetEnumerator   fe      = dt ? dt.fields().getEnumerator() : null;
        #
        #    return fe;
        #}
      ENDSOURCE
      SOURCE #fieldIdWithEDT
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// находит id n-ого поля с указанным типом
        #// порядок полей может не совпадать с объявленным в AOT
        #public static FieldId fieldIdWithEDT(TableId tableId, ExtendedTypeId edt, int occurrence = 1)
        #{
        #    SysDictField df = SysDictTable::fieldWithEDT(tableId, edt, occurrence);
        #
        #    if( df )
        #    {
        #        return df.id();
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #fieldIdWithEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// находит id n-ого поля с указанным enum
        #// порядок полей может не совпадать с объявленным в AOT
        #public static FieldId fieldIdWithEnum(TableId tableId, EnumId enumId, int occurrence = 1)
        #{
        #    SysDictField df = SysDictTable::fieldWithEnum(tableId, enumId, occurrence);
        #
        #    if( df )
        #    {
        #        return df.id();
        #    }
        #
        #    return 0;
        #}
      ENDSOURCE
      SOURCE #fieldNames
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// Внимание! наименования полей могут совпадать, поэтому set названий полей может быть меньше, чем полей в таблице
        #// используйте с осторожностью
        #public static Set fieldNames(tableId tableId, boolean includeSystem = false)
        #{
        #    Set             set     = new Set(Types::String);
        #    SetEnumerator   fe      = SysDictTable::fieldEnumerator(tableId);
        #    SysDictField    df;
        #    ;
        #
        #    while( fe && fe.moveNext() )
        #    {
        #        df = fe.current();
        #
        #        if( df && (includeSystem || !df.isSystem()) )
        #        {
        #            set.add(df.name());
        #        }
        #    }
        #
        #    return set;
        #}
      ENDSOURCE
      SOURCE #fieldWithEDT
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// находит n-ое поле с указанным типом
        #// порядок полей может не совпадать с объявленным в AOT
        #// используйте SysDictTable.fieldsWithEDT, чтобы найти все поля
        #public static SysDictField fieldWithEDT(TableId tableId, ExtendedTypeId edt, int occurrence = 1)
        #{
        #    SysDictTable    dt       = new SysDictTable(tableId);
        #    Set             fields   = dt ? dt.fieldsWithEDT(edt) : null;
        #    SetEnumerator   se       = fields ? fields.getEnumerator() : null;
        #    SysDictField    df;
        #
        #    while( se && se.moveNext() && occurrence > 0 )
        #    {
        #        df = se.current();
        #        occurrence--;
        #    }
        #
        #    return df;
        #}
      ENDSOURCE
      SOURCE #fieldWithEnum
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// находит n-ое поле с указанным enum
        #// порядок полей может не совпадать с объявленным в AOT
        #// используйте SysDictTable.fieldsWithEDT, чтобы найти все поля
        #public static SysDictField fieldWithEnum(TableId tableId, EnumId enumId, int occurrence = 1)
        #{
        #    SysDictTable    dt       = tableId  ? new SysDictTable(tableId) : null;
        #    Set             fields   = dt       ? dt.fieldsWithEnum(enumId) : null;
        #    SetEnumerator   se       = fields   ? fields.getEnumerator() : null;
        #    SysDictField    df;
        #
        #    while( se && se.moveNext() && occurrence > 0 )
        #    {
        #        df = se.current();
        #        occurrence--;
        #    }
        #
        #    return df;
        #}
      ENDSOURCE
      SOURCE #formRefName
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static MenuItemName formRefName(TableId tableId)
        #{
        #    DictTable dt = new DictTable(tableId);
        #
        #    if( dt )
        #    {
        #        return dt.formRef();
        #    }
        #
        #    return '';
        #}
      ENDSOURCE
      SOURCE #getMandatoryFields
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #static client server List getMandatoryFields(tableId _tableId)
        #{
        #    List            fieldIdList     = new List(Types::Integer);
        #    SysDictTable    sysDictTable    = new SysDictTable(_tableId);
        #    SysDictField    sysDictField;
        #    int             i;
        #
        #    for (i=1;i<=sysDictTable.fieldCnt();i++)
        #    {
        #        sysDictField = new SysDictField(_tableId, sysDictTable.fieldCnt2Id(i));
        #        if( sysDictField && sysDictField.saveContents() && sysDictField.mandatory() )
        #        {
        #            fieldIdList.addEnd(sysDictField.id());
        #        }
        #    }
        #
        #    return fieldIdList;
        #}
      ENDSOURCE
      SOURCE #is
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static boolean is(Common rec, TableId tableOrMapId)
        #{
        #    if( rec.TableId == tableOrMapId )
        #        return true;
        #
        #    if( tableOrMapId && SysDictTable::isTableMapped(tableOrMapId, rec.TableId) )
        #        return true;
        #
        #    return false;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: Timer unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #Timer
    PROPERTIES
      Name                #Timer
      Extends             #SysStopwatch
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class Timer extends SysStopwatch
        #{
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toString()
        #{
        #    return this.elapsed();
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Timer construct()
        #{
        #    Timer timer = new Timer();
        #
        #    return timer;
        #}
        #
      ENDSOURCE
      SOURCE #startNew
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static Timer startNew()
        #{
        #    Timer timer = Timer::construct();
        #    ;
        #
        #    timer.start();
        #
        #    return timer;
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysStopwatch unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysStopwatch
    PROPERTIES
      Name                #SysStopwatch
      Extends             #
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// класс обертка класса System.Diagnostics.Stopwatch
        #// см. https://docs.microsoft.com/en-us/dotnet/api/system.diagnostics.stopwatch
        #//
        #// быстрый и эффективный класс:
        #// + не задействует метод new(), поэтому можно спокойно использовать classFactory.createClass() и другие фабрики
        #// + объекты создаются в памяти только по необходимости
        #// + минимум вычислений на X++, форматирование и преобразование в строку выполняется в .net
        #// + скрывает взаимодействие с CLR - программист может просто использовать X++ класс в своем коде
        #// + метод stopwatch() возвращает CLR-объект  - программист может получить доступ ко всей функциональности .net-объекта
        #// + метод toString() позволяет видеть время выполнения в отладчике
        #//
        #class SysStopwatch
        #{
        #    System.Diagnostics.Stopwatch stopwatch;
        #}
      ENDSOURCE
      SOURCE #elapsed
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает строку со временем выполнения в формате [-][d.]hh:mm:ss (без миллисекунд)
        #public str elapsed()
        #{
        #    str   ret;
        #    int64 ticks;
        #    int64 ticksPerSecond = 10000000; // https://docs.microsoft.com/en-us/dotnet/api/system.timespan.tickspersecond
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    ticks = stopwatch.get_ElapsedTicks();
        #    ticks -= ticks mod ticksPerSecond;  // отбросим миллисекунды
        #
        #    ret = System.TimeSpan::FromTicks(ticks).ToString();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #ElapsedMilliseconds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public int64 elapsedMilliseconds()
        #{
        #    int64 ret;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    ret = stopwatch.get_ElapsedMilliseconds();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #elapsedTicks
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public int64 elapsedTicks()
        #{
        #    int64 ret;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    ret = stopwatch.get_ElapsedTicks();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #elapsedWide
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #// возвращает строку со временем выполнения в формате [-][d.]hh:mm:ss[.ffffff] (с миллисекундами)
        #public str elapsedWide()
        #{
        #    str ret;
        #    System.TimeSpan timespan;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    timespan = stopwatch.get_Elapsed();
        #
        #    ret = timespan.ToString();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #ensureInitialized
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #protected void ensureInitialized()
        #{
        #    if( !stopwatch )
        #    {
        #        stopwatch = new System.Diagnostics.Stopwatch();
        #    }
        #}
      ENDSOURCE
      SOURCE #isRunning
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public boolean isRunning()
        #{
        #    boolean ret;
        #
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    ret = stopwatch.get_IsRunning();
        #
        #    return ret;
        #}
      ENDSOURCE
      SOURCE #reset
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysStopwatch reset()
        #{
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    stopwatch.Reset();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #start
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysStopwatch start()
        #{
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    stopwatch.Start();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #stop
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public SysStopwatch stop()
        #{
        #    new InteropPermission(InteropKind::ClrInterop).assert();
        #
        #    this.ensureInitialized();
        #
        #    stopwatch.Stop();
        #
        #    return this;
        #}
      ENDSOURCE
      SOURCE #stopwatch
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public System.Diagnostics.Stopwatch stopwatch()
        #{
        #    this.ensureInitialized();
        #
        #    return stopwatch;
        #}
      ENDSOURCE
      SOURCE #toString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public str toString()
        #{
        #    return this.elapsed();
        #}
      ENDSOURCE
      SOURCE #construct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysStopwatch construct()
        #{
        #    return new SysStopwatch();
        #}
        #
      ENDSOURCE
      SOURCE #startNew
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #public static SysStopwatch startNew()
        #{
        #    return SysStopwatch::construct().start();
        #}
        #
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: CLS

; Microsoft Dynamics AX Class: SysStopwatchTest unloaded
; --------------------------------------------------------------------------------
  CLSVERSION 1
  
  CLASS #SysStopwatchTest
    PROPERTIES
      Name                #SysStopwatchTest
      Extends             #SysTestCase
      RunOn               #Called from
    ENDPROPERTIES
    
    METHODS
      Version: 3
      SOURCE #classDeclaration
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #class SysStopwatchTest extends SysTestCase
        #{
        #}
      ENDSOURCE
      SOURCE #testConstruct
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testConstruct()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #
        #    this.assertNotNull(stopwatch);
        #    this.assertTrue(SysDictClass::is(stopwatch, classnum(SysStopwatch)));
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testElapsed
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testElapsed()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #    str elapsed;
        #
        #    sleep(50);
        #    elapsed = stopwatch.elapsed();
        #
        #    this.assertTrue(match(@'^:d:d\::d:d\::d:d$', elapsed), elapsed);
        #}
      ENDSOURCE
      SOURCE #testElapsed_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testElapsed_NotStarted()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #
        #    this.assertNotNull(stopwatch);
        #    this.assertEquals('00:00:00', stopwatch.elapsed());
        #}
      ENDSOURCE
      SOURCE #testElapsedMilliseconds
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testElapsedMilliseconds()
        #{
        #    SysStopwatch sysStopWatch = SysStopWatch::startNew();
        #    int64 zero = 0;
        #;
        #    sleep(50);
        #
        #    this.assertNotNull(sysStopWatch);
        #    this.assertNotEqual(zero, sysStopWatch.elapsedMilliseconds());
        #}
      ENDSOURCE
      SOURCE #testElapsedMilliseconds_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testElapsedMilliseconds_NotStarted()
        #{
        #    SysStopwatch sysStopWatch = SysStopWatch::construct();
        #    int64 zero = 0;
        #;
        #    this.assertNotNull(sysStopWatch);
        #    this.assertEquals(zero, sysStopWatch.elapsedMilliseconds());
        #}
      ENDSOURCE
      SOURCE #testElapsedWideFormat
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testElapsedWideFormat()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #    str elapsed;
        #
        #    sleep(50);
        #    elapsed = stopwatch.elapsedWide();
        #
        #    this.assertTrue(match(@'^:d:d\::d:d\::d:d\.:d+$', elapsed), elapsed);
        #}
      ENDSOURCE
      SOURCE #testElapsedWideFormat_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testElapsedWideFormat_NotStarted()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #
        #    this.assertNotNull(stopwatch);
        #    this.assertEquals('00:00:00', stopwatch.elapsedWide());
        #}
      ENDSOURCE
      SOURCE #testIsRunning
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsRunning()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #
        #    sleep(50);
        #
        #    this.assertTrue(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testIsRunning_Immediately
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsRunning_Immediately()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #
        #    this.assertTrue(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testIsRunning_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsRunning_NotStarted()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testIsRunning_Reset
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsRunning_Reset()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #
        #    stopwatch.reset();
        #
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testIsRunning_Start
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsRunning_Start()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #
        #    this.assertTrue(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testIsRunning_Stop
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testIsRunning_Stop()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testReset
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testReset()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #    int64 zero = 0;
        #
        #    sleep(50);
        #    stopwatch.reset();
        #
        #    this.assertEquals(zero, stopwatch.elapsedMilliseconds());
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testReset_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testReset_NotStarted()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #    int64 zero = 0;
        #
        #    this.assertEquals(zero, stopwatch.elapsedMilliseconds());
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testStartNew
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testStartNew()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #
        #    this.assertNotNull(stopwatch);
        #    this.assertTrue(SysDictClass::is(stopwatch, classnum(SysStopwatch)));
        #    this.assertTrue(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testStop
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testStop()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #    int64 zero = 0;
        #
        #    sleep(50);
        #    stopwatch.stop();
        #
        #    this.assertNotEqual(zero, stopwatch.elapsedMilliseconds());
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testStop_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testStop_NotStarted()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #    int64 zero = 0;
        #
        #    sleep(50);
        #    stopwatch.stop();
        #
        #    this.assertEquals(zero, stopwatch.elapsedMilliseconds());
        #    this.assertFalse(stopwatch.isRunning());
        #}
      ENDSOURCE
      SOURCE #testToString
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testToString()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::startNew();
        #    str elapsed;
        #
        #    sleep(50);
        #    elapsed = stopwatch.elapsed();
        #
        #    this.assertTrue(match(@':d:d\::d:d\::d:d$', elapsed), elapsed);
        #}
      ENDSOURCE
      SOURCE #testToString_NotStarted
        #//
        #// https://github.com/mazzy-ax/SysUtil
        #//
        #void testToString_NotStarted()
        #{
        #    SysStopwatch stopwatch = SysStopwatch::construct();
        #
        #    this.assertEquals('00:00:00', stopwatch.toString());
        #}
      ENDSOURCE
    ENDMETHODS
  ENDCLASS

***Element: PRN

; Microsoft Dynamics AX Project : SysUtil unloaded
; --------------------------------------------------------------------------------
  PROJECTVERSION 2
  
  PROJECT #SysUtil
   SHARED
  PROPERTIES
    Name                #SysUtil
  ENDPROPERTIES
  
    PROJECTCLASS ProjectNode
    GROUP #Any
      PROPERTIES
        Name                #Any
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50014
        NODETYPE 329
        NAME #Any
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50016
        NODETYPE 329
        NAME #AnyTest
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50015
        NODETYPE 329
        NAME #AnyPrevCurrent
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50017
        NODETYPE 329
        NAME #AnytypeUtil
      ENDNODE
      ENDGROUP
      
    GROUP #Args
      PROPERTIES
        Name                #Args
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50019
        NODETYPE 329
        NAME #ArgsUtil
      ENDNODE
      ENDGROUP
      
    GROUP #Collection
      PROPERTIES
        Name                #Collection
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50023
        NODETYPE 329
        NAME #ConUtil
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50025
        NODETYPE 329
        NAME #ListUtil
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50029
        NODETYPE 329
        NAME #SetUtil
      ENDNODE
      ENDGROUP
      
    GROUP #CLR
      PROPERTIES
        Name                #CLR
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50020
        NODETYPE 329
        NAME #ClrTypeUtil
      ENDNODE
      ENDGROUP
      
    GROUP #Enumerator
      PROPERTIES
        Name                #Enumerator
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50021
        NODETYPE 329
        NAME #EnumeratorUtil
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50022
        NODETYPE 329
        NAME #ConEnumerator
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50026
        NODETYPE 329
        NAME #OneValueEnumerator
      ENDNODE
      ENDGROUP
      
    GROUP #Error
      PROPERTIES
        Name                #Error
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 123
        NODETYPE 329
        NAME #Error
      ENDNODE
      ENDGROUP
      
    GROUP #Query
      PROPERTIES
        Name                #Query
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 2881
        NODETYPE 329
        NAME #SysQuery
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50032
        NODETYPE 329
        NAME #SysQueryTest
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50027
        NODETYPE 329
        NAME #QueryRunUtil
      ENDNODE
      ENDGROUP
      
    GROUP #Record
      PROPERTIES
        Name                #Record
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50028
        NODETYPE 329
        NAME #RecordUtil
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50034
        NODETYPE 329
        NAME #SysRecordList
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50035
        NODETYPE 329
        NAME #SysRecordMap
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50033
        NODETYPE 329
        NAME #SysRecordInsertList
      ENDNODE
      ENDGROUP
      
    GROUP #Str
      PROPERTIES
        Name                #Str
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50030
        NODETYPE 329
        NAME #StrUtil
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50036
        NODETYPE 329
        NAME #TextBufferUtil
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50037
        NODETYPE 329
        NAME #TextUtil
      ENDNODE
      ENDGROUP
      
    GROUP #Session
      PROPERTIES
        Name                #Session
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50013
        NODETYPE 329
        NAME #SessionUtil
      ENDNODE
      ENDGROUP
      
    GROUP #SysDict
      PROPERTIES
        Name                #SysDict
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 1568
        NODETYPE 329
        NAME #SysDictClass
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 982
        NODETYPE 329
        NAME #SysDictEnum
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 935
        NODETYPE 329
        NAME #SysDictTable
      ENDNODE
      ENDGROUP
      
    GROUP #Timer
      PROPERTIES
        Name                #Timer
        ProjectGroupType    #Classes
        GroupMask           #
        PreventEditProperties #No
      ENDPROPERTIES
      
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50038
        NODETYPE 329
        NAME #Timer
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50001
        NODETYPE 329
        NAME #SysStopwatch
      ENDNODE
      BEGINNODE
        FILETYPE 0
        UTILTYPE 45
        UTILOBJECTID 50002
        NODETYPE 329
        NAME #SysStopwatchTest
      ENDNODE
      ENDGROUP
      
  ENDPROJECT
  

***Element: END

//
// https://github.com/mazzy-ax/SysUtil
//
// enumerator для ОДНОГО значения произвольного типа
class OneValueEnumerator implements Enumerator
{
    anytype currentValue;
    int pos; // 0: before, 1: inside, >1: after. Inside if (pos == 1)

    public anytype current()
    {
        if( pos == 1 )
        {
            return currentValue;
        }

        return AnytypeUtil::undefined();
    }

    public str definitionString()
    {
        return strfmt('anytype[%1] enumerator', typeof(currentValue));
    }

    // метод публичный, чтобы объект данного класса можно было создавать из classFactory и инициализировать из всевозможных unpack
    // повторный вызов этого метода не приведет к изменению типа переменной currentValue. И не должен приводить.
    // не вызывайте этот метод напрямую.
    public void init(anytype value)
    {
        currentValue = value;
    }


    public boolean moveNext()
    {
        pos++;

        return (pos == 1);
    }

    public void new(anytype value)
    {
        this.init(value);
    }

    public void reset()
    {
        pos = 0;
    }

    public str toString()
    {
        if( pos < 1 )
        {
            return '(before)';
        }
        else if( pos > 1 )
        {
            return '(end)';
        }

        return strfmt('[%1]', currentValue);
    }

    public static OneValueEnumerator construct(anytype value)
    {
        return new OneValueEnumerator(value);
    }

}
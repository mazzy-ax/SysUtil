//
// https://github.com/mazzy-ax/SysUtil
//
// Класс обслуживает объекты типа Args
//
// Util-класс реализует только статические методы.
// не содержит внутри никаких значений.
// не имеет смысла создавать экземпляр этого класса.
//
abstract class ArgsUtil
{

    public static Common asRecord(Args args, TableId tableId = 0)
    {
        Common ret;

        if( ArgsUtil::checkRecord(args, tableId) )
        {
            ret = args.record();
        }

        return ret;
    }

    public static void assertCallerFormName(Args args, FormName formName, str _funcname = '')
    {
        if( ArgsUtil::callerFormName(args) == formName)
        {
            return;
        }

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    public static void assertCallerType(Args args, classId potentialAncestorClassId, str _funcname = '')
    {
        if( ArgsUtil::checkCallerType(args, potentialAncestorClassId) )
        {
            return;
        }

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    public static void assertEnum(Args args, Anytype enum, str _funcname = '')
    {
        if ( ArgsUtil::checkEnum(args, enum) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static void assertEnum_Parm(Args args, Anytype enum, str _funcname = '')
    {
        if ( ArgsUtil::checkEnum_Parm(args, enum) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static void assertEnum_RefField(Args args, Anytype enum, str _funcname = '')
    {
        if ( ArgsUtil::checkEnum_RefField(args, enum) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static void assertEnumType(Args args, EnumId enumId, str _funcname = '')
    {
        if( ArgsUtil::checkEnumType(args, enumId) )
        {
            return;
        }

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    public static void assertEnumType_Parm(Args args, EnumId enumId, str _funcname = '')
    {
        if( ArgsUtil::checkEnumType_Parm(args, enumId) )
        {
            return;
        }

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    public static void assertEnumType_RefField(Args args, EnumId enumId, str _funcname = '')
    {
        if( ArgsUtil::checkEnumType_RefField(args, enumId) )
        {
            return;
        }

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    public static void assertFormDatasource(Args args, str _funcname = '')
    {
        if (args && args.dataset() && args.record().isFormDataSource())
        {
            return;
        }

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    public static void assertParm(Args args, str matchPattern = '.', str _funcname = '')
    {
        if ( ArgsUtil::checkParm(args, matchPattern) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static void assertRecord(Args args, TableId tableId = 0, str _funcname = '')
    {
        if ( ArgsUtil::checkRecord(args, tableId) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static void assertRecordExists(Args args, TableId tableId = 0, str _funcname = '')
    {
        if ( ArgsUtil::checkRecordExists(args, tableId) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static void assertRecordTableIds(Args args, container tableIds, str _funcname = '')
    {
        if ( ArgsUtil::checkRecordTableIds(args, tableIds) )
        {
            return;
        }

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    public static Object caller(Args args, classId potentialAncestorClassId)
    {
        Object ret;

        if( args )
        {
            ret = SysDictClass::as(args.caller(), potentialAncestorClassId);
        }

        return ret;
    }

    public static str callerFormName(Args args)
    {
        FormRun formRun = ArgsUtil::caller(args, classnum(FormRun));
        str formName;

        if( formRun )
        {
            formName = formRun.form().name();
        }

        return formName;
    }

    public static boolean checkCallerFormName(Args args, str formName)
    {
        boolean ret = (ArgsUtil::callerFormName(args) == formName);

        return ret;
    }

    public static boolean checkCallerType(Args args, classId potentialAncestorClassId)
    {
        boolean checkOk = args && SysDictClass::is(args.caller(), potentialAncestorClassId);

        return checkOk;
    }

    public static boolean checkEnum(Args args, Anytype enum)
    {
        boolean ret = ArgsUtil::checkEnum_Parm(args, enum) ||
                      ArgsUtil::checkEnum_RefField(args, enum);

        return ret;
    }


    public static boolean checkEnum_Parm(Args args, Anytype enum)
    {
        boolean ret;

        if( args && typeof(enum) == types::Enum )
        {
            ret = (enum == args.parmEnum());
        }

        return ret;
    }


    // TODO!!!!
    public static boolean checkEnum_RefField(Args args, Anytype enum)
    {
        boolean ret;

        if( args && typeof(enum) == types::Enum )
        {
            throw error(Error::missingOverride(funcname()));

            ret = (enum == args.parmEnum());
        }

        return ret;
    }


    public static boolean checkEnumType(Args args, EnumId enumId)
    {
        boolean ret = ArgsUtil::checkEnumType_Parm(args, enumId) ||
                      ArgsUtil::checkEnumType_RefField(args, enumId);

        return ret;
    }

    public static boolean checkEnumType_Parm(Args args, EnumId enumId)
    {
        boolean checkOk = (args && args.parmEnumType() == enumId);

        return checkOk;
    }

    public static boolean checkEnumType_RefField(Args args, EnumId enumId)
    {
        boolean checkOk = (args && args.parmEnumType() == enumId);

        return checkOk;
    }

    // параметр parm содержит паттерн.
    // возвращает true, если parm-строка соответствует regexp-выражению в matchPattern (метод использует функцию match для проверки)
    // если не указать matchPattern, то возвращает true, если parm не пустой (содержит хотя бы один символ)
    // возвращает true, если matchPattern является пустой строкой
    public static boolean checkParm(Args args, str matchPattern = '.')
    {
        boolean checkOk = (args && match(matchPattern, args.parm()));

        return checkOk;
    }


    // запись из указанной таблицы (или любой таблицы для tableId = 0)
    public static boolean checkRecord(Args args, TableId tableId = 0)
    {
        boolean ret = false;
        int dataset;

        if( args )
        {
            dataset = args.dataset();

            if( dataset )
            {
                ret = (tableId == 0 || TableUtil::is(dataset, tableId));
            }
        }

        return ret;
    }

    // запись существует (recId != 0) и запись из указанной таблицы (или любой таблицы для tableId = 0)
    public static boolean checkRecordExists(Args args, TableId tableId = 0)
    {
        boolean checkOk = (ArgsUtil::checkRecord(args, tableId) && args.record().RecId != 0);

        return checkOk;
    }

    // возвращает true, если:
    // * коллекция содержит tableId, совпадающий с tableId таблицы
    // * коллекция содержит 0 или CommonTableId
    // * коллекция пустая и записи в args не переданы
    // возвращает false в остальных случаях

    // коллекция - это одно значение, container, Set, List, Array
    public static boolean checkRecordTableIds(Args args, anytype tableIdOrCollection)
    {
        boolean     ret = true; // для пустой коллекции
        Enumerator  e   = EnumeratorUtil::getEnumerator(tableIdOrCollection);
        TableId     tableId;

        while( e && e.moveNext() )
        {
            tableId = e.current(); // TODO здесь возможно исключение, если коллекция содержит не целые

            if( ArgsUtil::checkRecord(args, tableId) )
            {
                return true;
            }

            ret = false;
        }

        return ret;
    }

    public static Args copyArgs(Args fromArgs, Args toArgs = null)
    {
        if (fromArgs)
        {
            if (!toArgs)
            {
                toArgs = new Args();
            }

            SysArgs::copyArgs(fromArgs, toArgs);
        }

        return toArgs;
    }


    public static TableId dataset(Args args)
    {
        TableId dataset = 0;

        if( args )
        {
            dataset = args.dataset();
        }

        return dataset;
    }

    // убедиться что в аргументах enum указанного типа и
    // вернуть значение этого enum из аргументов
    // бросает исключение, если enum не указанного типа
    public static int enum(Args args, anytype enumIdNameOrValue, str _funcname = '')
    {
        EnumId enumId = AnytypeUtil::enumId(enumIdNameOrValue);
        ;

        ArgsUtil::assertEnumType(args, enumId, _funcname);

        return args.parmEnum();
    }

    // убедиться что в аргументах CopyOfenum указанного типа и
    // вернуть значение этого enum из аргументов
    // бросает исключение, если enum не указанного типа
    public static int enum_Parm(Args args, EnumId enumId, str _funcname = '')
    {;
        ArgsUtil::assertEnumType_Parm(args, enumId, _funcname);

        return args.parmEnum();
    }

    // убедиться что в аргументах enum указанного типа и
    // вернуть значение этого enum из аргументов
    // бросает исключение, если enum не указанного типа
    public static int enum_RefField(Args args, EnumId enumId, str _funcname = '')
    {;
        ArgsUtil::assertEnumType_RefField(args, enumId, _funcname);

        return args.parmEnum(); // TODO !!!!!
    }

    // TODO !!!
    // тоже самое, что и ArgsUtil::getEnum()
    // но если в аргументах не нужный enum не выдает ошибку, а возвращает дефолтное значение.
    public static int enumOrDefault(Args args, anytype defaultEnumValue)
    {
        if( typeof(defaultEnumValue) == Types::Enum )
        {
            if( ArgsUtil::checkEnumType(args, AnytypeUtil::enumId(defaultEnumValue)) )
            {
                return args.parmEnum();
            }

            return defaultEnumValue;
        }

        throw error::wrongUseOfFunction(funcname());
    }

    // тоже самое, что и ArgsUtil::getEnumFromParm()
    // но если в аргументах не нужный enum не выдает ошибку, а возвращает дефолтное значение.
    public static int enumOrDefault_Parm(Args args, anytype defaultEnumValue)
    {
        if( typeof(defaultEnumValue) == Types::Enum )
        {
            if( ArgsUtil::checkEnumType_Parm(args, AnytypeUtil::enumId(defaultEnumValue)) )
            {
                return args.parmEnum();
            }

            return defaultEnumValue;
        }

        throw error::wrongUseOfFunction(funcname());
    }

    // тоже самое, что и ArgsUtil::getEnumFromParm()
    // но если в аргументах не нужный enum не выдает ошибку, а возвращает дефолтное значение.
    public static int enumOrDefault_RefField(Args args, anytype defaultEnumValue)
    {
        if( typeof(defaultEnumValue) == Types::Enum )
        {
            if( ArgsUtil::checkEnumType_RefField(args, AnytypeUtil::enumId(defaultEnumValue)) )
            {
                return args.parmEnum(); // TODO !!!!
            }

            return defaultEnumValue;
        }

        throw error::wrongUseOfFunction(funcname());
    }

    // возвращает отмеченные в датасорсе записи
    // список может быть пустым, если record не является датасорсом формы
    public static RecordList marked(Args args, TableId tableId = 0)
    {
        RecordList ret;

        if( ArgsUtil::checkRecord(args, tableId) )
        {
            ret = RecordList::fromMarked(args.record().dataSource());
        }

        return ret;
    }

    // возвращает отмеченные на форме записи или текущую
    public static RecordList markedOrCurrent(Args args, TableId tableId = 0)
    {
        RecordList ret = ArgsUtil::marked(args, tableId);
        Common buf;
        ;

        if( !ret || ret.elements() == 0 )
        {
            buf = ArgsUtil::record(args, tableId);
            ret = RecordList::fromBuf1(buf);
        }

        return ret;
    }

    public static container markedRecIds(Args args, TableId tableId = 0, str _funcname = '')
    {
        container       ret;
        FormDataSource  fds;
        common          record;

        if( ArgsUtil::checkRecord(args, tableId) )
        {
            fds = args.record().dataSource();
            if( fds && fds.anyMarked() )
            {
                // marked records
                for( record = fds.getFirst(true) ; record ; record = fds.getNext() )
                {
                    ret += record.RecId;
                }
            }

            if( !ret )
            {
                // or a record itself
                record = ArgsUtil::record(args, tableId, _funcname);
                ret = [record.RecId];
            }
        }

        return ret;
    }

    // возвращает set со значениями в указанном поле в промаркированных на форме записях
    // если args не содержит записей или содержит записи "не того" типа, то null
    // если на форме нет промарктированных записей, то вернет set из одной записи (из args.record)
    // если опустить параметр _field, то возвратит значения recId
    public static Set markedSet(Args args, TableId tableId = 0, FieldId fieldId = 0)
    {
        Set             ret;
        Common          record = ArgsUtil::asRecord(args, tableId);
        FormDataSource  fds;
        Common          marked;

        if( RecordUtil::isNotCommon(record) )
        {
            fieldId  = nz(fieldId, fieldnum(Common, RecId));

            fds = record.dataSource();
            if( fds && fds.anyMarked() )
            {
                // marked records
                for( marked = fds.getFirst(true) ; marked ; marked = fds.getNext() )
                {
                    ret = SetUtil::addFieldValue(ret, marked, fieldId);
                }
            }

            if( !ret )
            {
                // or a record itself
                ret = SetUtil::addFieldValue(ret, record, fieldId);
            }
        }

        return ret;
    }

    // возвращает map(key=fieldId, value=set), содержащий set со значениями в указанном поле в промаркированных на форме записях
    // если args не содержит записей или содержит записи "не того" типа, то вернет пустой map
    // если опустить параметр _field, то возвратит значения одного поля - recId
    // если на форме нет промарктированных записей, то вернет набор из одной записи (из args.record)
    public static Map markedSets(Args args, TableId tableId, anytype fieldIdCollection)
    {
        Map         ret = new Map(Types::Integer, Types::Class); // {FieldId, Set(FieldValue)}
        Enumerator  e;
        FieldId     fieldId;
        Set         set;

        if( ArgsUtil::checkRecord(args, tableid) )
        {
            // можно переделать, чтобы вместо вложенных циклов был один цикл
            e = AnytypeUtil::getEnumerator(fieldIdCollection);
            while( tableId && e && e.moveNext() )
            {
                fieldId = AnytypeUtil::asInt(e.current());
                if( fieldId )
                {
                    set = ArgsUtil::markedSet(args, tableId, fieldId);
                    ret.insert(fieldId, set);
                }
            }
        }

        return ret;
    }

    // заполняет данные в указанной записи, если в аргументах есть запись с подходящей таблицей (map)
    // возвращает true, если запись была заполнена
    //
    // Внимание! метод заполнит только поля. метод потеряет информацию о datasource, while select, selectForUpdate и прочее
    public static boolean populateRecord(Args args, Common record)
    {
        boolean ret;

        if( ArgsUtil::checkRecord(args, record.TableId) )
        {
            record.data(args.record());
            ret = true;
        }

        return ret;
    }

    public static Common record(Args args, TableId tableId = 0, str _funcname = '')
    {;
        ArgsUtil::assertRecord(args, tableId, _funcname);

        return args.record();
    }

    public static boolean setEnum(Args args, anytype enum)
    {
        boolean ret;
        EnumId enumId;

        if( args && typeof(enum) == Types::Enum )
        {
            enumId = DictEnum::value2id(enum);
            args.parmEnumType(enumId);
            args.parmEnum(enum);

            ret = true;
        }

        return ret;
    }
}
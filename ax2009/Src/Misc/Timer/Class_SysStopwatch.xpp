//
// https://github.com/mazzy-ax/SysUtil
//
// класс обертка класса System.Diagnostics.Stopwatch
// см. https://docs.microsoft.com/en-us/dotnet/api/system.diagnostics.stopwatch
//
// быстрый и эффективный класс:
// + не задействует метод new(), поэтому можно спокойно использовать classFactory.createClass() и другие фабрики
// + объекты создаются в памяти только по необходимости
// + минимум вычислений на X++, форматирование и преобразование в строку выполняется в .net
// + скрывает взаимодействие с CLR - программист может просто использовать X++ класс в своем коде
// + метод stopwatch() возвращает CLR-объект  - программист может получить доступ ко всей функциональности .net-объекта
// + метод toString() позволяет видеть время выполнения в отладчике
//
class SysStopwatch
{
    System.Diagnostics.Stopwatch stopwatch;

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    // возвращает строку со временем выполнения в формате [-][d.]hh:mm:ss (без миллисекунд)
    public str elapsed()
    {
        str   ret;
        int64 ticks;
        int64 ticksPerSecond = 10000000; // https://docs.microsoft.com/en-us/dotnet/api/system.timespan.tickspersecond

        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        ticks = stopwatch.get_ElapsedTicks();
        ticks -= ticks mod ticksPerSecond;  // отбросим миллисекунды

        ret = System.TimeSpan::FromTicks(ticks).ToString();

        return ret;
    }

    public str elapsedFormated(boolean _stop = false)
    {
        System.TimeSpan ts;
        str             ret;
        ;

        if (_stop)
        {
            this.stop();
        }

        new InteropPermission(InteropKind::ClrInterop).assert();

        if (stopwatch)
        {
            ts = stopwatch.get_Elapsed();
        }
        else
        {
            ts = System.TimeSpan::FromTicks(0);
        }

        if (ts)
        {
            ret = ts.ToString();
        }

        CodeAccessPermission::revertAssert();

        return ret;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public int64 elapsedMilliseconds()
    {
        int64 ret;

        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        ret = stopwatch.get_ElapsedMilliseconds();

        return ret;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public int64 elapsedTicks()
    {
        int64 ret;

        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        ret = stopwatch.get_ElapsedTicks();

        return ret;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    // возвращает строку со временем выполнения в формате [-][d.]hh:mm:ss[.ffffff] (с миллисекундами)
    public str elapsedWide()
    {
        str ret;
        System.TimeSpan timespan;

        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        timespan = stopwatch.get_Elapsed();

        ret = timespan.ToString();

        return ret;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    protected void ensureInitialized()
    {
        if( !stopwatch )
        {
            stopwatch = new System.Diagnostics.Stopwatch();
        }
    }

    protected void init()
    {;
        new InteropPermission(InteropKind::ClrInterop).assert();

        if (stopwatch)
        {
            stopwatch = new System.Diagnostics.Stopwatch();
        }

        CodeAccessPermission::revertAssert();
    }

    protected void initAndStart()
    {;
        new InteropPermission(InteropKind::ClrInterop).assert();

        stopwatch = System.Diagnostics.Stopwatch::StartNew();

        CodeAccessPermission::revertAssert();
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public boolean isRunning()
    {
        boolean ret;

        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        ret = stopwatch.get_IsRunning();

        return ret;
    }

    protected void new(boolean startNow = false)
    {
        if (startNow)
        {
            this.initAndStart();
        }
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public SysStopwatch reset()
    {
        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        stopwatch.Reset();

        return this;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public SysStopwatch start()
    {
        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        stopwatch.Start();

        return this;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public SysStopwatch stop()
    {
        new InteropPermission(InteropKind::ClrInterop).assert();

        this.ensureInitialized();

        stopwatch.Stop();

        return this;
    }

    public int64 stopPrintElapsed(boolean _formated = true, str _text = '')
    {
        int64 ret;
        ;

        this.stop();

        ret = this.elapsedMilliseconds();

        if (!_text)
        {
            _text = 'ELAPSED: ';
        }

        if (_formated)
        {
            debug::printDebug(_text, this.elapsedFormated());
        }
        else
        {
            debug::printDebug(_text, ret, " MS");
        }

        return ret;

    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public System.Diagnostics.Stopwatch stopwatch()
    {
        this.ensureInitialized();

        return stopwatch;
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public str toString()
    {
        return this.elapsed();
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public static SysStopwatch construct()
    {
        return new SysStopwatch();
    }


    public static SysStopWatch contruct(boolean startNow = false)
    {
        return new SysStopWatch(startNow);
    }

    //
    // https://github.com/mazzy-ax/SysUtil
    //
    public static SysStopwatch startNew()
    {
        return SysStopwatch::construct().start();
    }

}